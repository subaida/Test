 /**
* @class MultiAxisChart
* @author InfoSoft Global (P) Ltd. www.InfoSoftGlobal.com
* @version 3.0
*
* Copyright (C) InfoSoft Global Pvt. Ltd. 2005-2006
*
* MultiAxisChart chart extends the Chart class to render a
* Multi-Axis 2D Line Chart.
*/
//Import Chart class
import com.fusioncharts.core.Chart;
//Parent SingleYAxis2DVerticalChart Class
import com.fusioncharts.core.SingleYAxis2DVerticalChart;
//VerticalAxis Class
import com.fusioncharts.helper.VerticalAxis;
//Error class
import com.fusioncharts.helper.FCError;
//Import Logger Class
import com.fusioncharts.helper.Logger;
//Style Object
import com.fusioncharts.core.StyleObject;
//Delegate
import mx.utils.Delegate;
//Legend Class
import com.fusioncharts.helper.Legend;
//Extensions
import com.fusioncharts.extensions.StringExt;
import com.fusioncharts.extensions.DrawingExt;
//Drop Shadow filter
import flash.filters.DropShadowFilter;
//Number formatting class
import com.fusioncharts.helper.NumberFormatting;
class com.fusioncharts.core.charts.MultiAxisLineChart extends SingleYAxis2DVerticalChart 
{
	//Version number (if different from super Chart class)
	//private var _version:String = "3.0.0";
	//Instance variables
	//List of chart objects
	private var _arrObjects : Array;
	private var xmlData : XML;
	//Array to store x-axis categories (labels)
	private var categories : Array;
	//Array to store axis information after parsing from XML
	private var axis : Array;
	//Number of Axis
	private var numAxis: Number;
	//Array to store object Instances of axis class initialized.
	private var vAxis:Array;
	//Number of data items
	private var num : Number;
	//Number of data axis on Left
	private var numAxisOnLeft: Number;
	//Number of right Axis
	private var numAxisOnRight : Number;
	//Arrays to store the axis index on side of canvas
	private var leftAxisArray:Array;
	private var rightAxisArray:Array;
	//Maximum number of DS
	private var maxDS: Number;
	//Reference to legend component of chart
	private var lgnd : Legend;
	//Reference to legend movie clip
	private var lgndMC : MovieClip;
	//Cursor MC
	private var cursorMC: MovieClip;
	//Listeners
	//Check Listener -  to hide and show the DataPlot
	var checkListener:Object;
	//Click Listener-  for axis movement
	var clickListener:Object;
	//Constant
	//Axis padding refers to the horizontal gap between two axis.
	//Each axis is considered as full object including label width,
	//checkbox width (with check box label).
	private var AXIS_PADDING:Number = 17;
	//MAX_TITLE_HEIGHT refers to the maximum vertical height for the 
	//axis title. If the actual height of the axis label crosses this value,
	//we cut it off. This is avoid overlapping with the legend.
	//This happens only in case of bottom alignment of check box label.
	private var MAX_TITLE_HEIGHT:Number = 40;
	/**
	* Constructor function. We invoke the super class'
	* constructor and then set the objects for this chart.
	*/
	function MultiAxisLineChart (targetMC : MovieClip, depth : Number, width : Number, height : Number, x : Number, y : Number, debugMode : Boolean, lang : String, scaleMode:String, registerWithJS:Boolean, DOMId:String)
	{
		//Invoke the super class constructor
		super (targetMC, depth, width, height, x, y, debugMode, lang, scaleMode, registerWithJS, DOMId);
		//Log additional information to debugger
		//We log version from this class, so that if this class version
		//is different, we can log it
		this.log ("Version", _version, Logger.LEVEL.INFO);
		this.log ("Chart Type", "Multi Axis 2D Line Chart", Logger.LEVEL.INFO);
		//List Chart Objects and set them
		_arrObjects = new Array ("BACKGROUND", "CANVAS", "CAPTION", "SUBCAPTION", "XAXISNAME", "DIVLINES", "VDIVLINES", "YAXISVALUES", "VGRID", "DATALABELS", "DATAVALUES", "DATAPLOT", "ANCHORS", "TOOLTIP", "VLINES", "LEGEND", "AXISTITLE");
		super.setChartObjects (_arrObjects);
		//Initialize the containers for chart
		this.categories = new Array ();
		//Initialize the axis array
		this.axis = new Array ();
		//Axis array Object
		this.vAxis= new Array ();
		//Initialize the index Array
		this.leftAxisArray = new Array();
		this.rightAxisArray = new Array();
		//Initialize the number of data elements present
		this.numAxis = 0;
		//Initialize the Numbers
		this.numAxisOnRight = 0;
		this.numAxisOnLeft = 0;
		this.num = 0;
		this.maxDS = 0;
		this.config.paddingReqd = false;
		//Initialize all the listeners
		this.checkListener = new Object();		
		//Click Listener
		this.clickListener = new Object();
	}
	/**
	* render method is the single call method that does the rendering of chart:
	* - Parsing XML
	* - Calculating values and co-ordinates
	* - Visual layout and rendering
	* - Event handling
	*/
	public function render () : Void 
	{
		//Parse the XML Data document
		this.parseXML ();
		//Now, if the number of data elements is 0, we show pertinent
		//error.
		if (this.numAxis == 0 || this.num == 0)
		{
			tfAppMsg = this.renderAppMessage (_global.getAppMessage ("NODATA", this.lang));
			//Add a message to log.
			this.log ("No Data to Display", "No data was found in the XML data document provided. Possible cases can be: <LI>There isn't any data generated by your system. If your system generates data based on parameters passed to it using dataURL, please make sure dataURL is URL Encoded.</LI><LI>You might be using a Single Series Chart .swf file instead of Multi-series .swf file and providing multi-series data or vice-versa.</LI>", Logger.LEVEL.ERROR);
			//Expose rendered method
			this.exposeChartRendered();
		} else 
		{
			//Detect number scales
			this.detectNumberScales ();
			//Set Style defaults
			this.setStyleDefaults ();
			//Allot the depths for various charts objects now
			this.allotDepths ();
			//Draw the cursor
			this.drawCursor();
			//Calculate Points
			this.calculatePoints ();
			//Calculate vLine Positions
			this.calcVLinesPos ();
			//Feed macro values
			super.feedMacros ();
			//Remove application message
			this.removeAppMessage (this.tfAppMsg);
			//Set tool tip parameter
			this.setToolTipParam ();
			//-----Start Visual Rendering Now------//
			//Draw background
			this.drawBackground ();
			//Set click handler
			this.drawClickURLHandler ();
			//Load background SWF
			this.loadBgSWF ();
			//Update timer
			this.timeElapsed = (this.params.animation) ? this.styleM.getMaxAnimationTime (this.objects.BACKGROUND) : 0;
			//Draw canvas
			this.config.intervals.canvas = setInterval (Delegate.create (this, drawCanvas) , this.timeElapsed);
			//Draw headers
			this.config.intervals.headers = setInterval (Delegate.create (this, drawHeaders) , this.timeElapsed);
			//Update timer
			this.timeElapsed += (this.params.animation) ? this.styleM.getMaxAnimationTime (this.objects.CANVAS, this.objects.CAPTION, this.objects.SUBCAPTION, this.objects.XAXISNAME) : 0;
			//Vertical div lines
			this.config.intervals.vDivLines = setInterval (Delegate.create (this, drawVDivLines) , this.timeElapsed);
			//Update timer
			this.timeElapsed += (this.params.animation && (this.params.numVDivLines > 0)) ? this.styleM.getMaxAnimationTime (this.objects.VDIVLINES) : 0;
			//Vertical grid
			this.config.intervals.vGrid = setInterval (Delegate.create (this, drawVGrid) , this.timeElapsed);
			//Update timer
			this.timeElapsed += (this.params.animation && (this.params.showAlternateVGridColor)) ? this.styleM.getMaxAnimationTime (this.objects.VGRID) : 0;
			//Draw Axis
			this.config.intervals.axis = setInterval (Delegate.create (this, drawAxis) , this.timeElapsed);
			//Update timer
			this.timeElapsed += (this.params.animation && (this.num > 1)) ? this.styleM.getMaxAnimationTime (this.objects.YAXISVALUES, this.objects.DIVLINES) : 0;
			//Draw labels
			this.config.intervals.labels = setInterval (Delegate.create (this, drawLabels) , this.timeElapsed);
			//Draw line chart
			this.config.intervals.plot = setInterval (Delegate.create (this, drawLineChart) , this.timeElapsed);
			//Legend
			this.config.intervals.legend = setInterval (Delegate.create (this, drawLegend) , this.timeElapsed);
			//Update timer
			this.timeElapsed += (this.params.animation && (this.num > 1)) ? this.styleM.getMaxAnimationTime (this.objects.DATALABELS, this.objects.DATAPLOT, this.objects.LEGEND) : 0;
			//Data Values
			this.config.intervals.dataValues = setInterval (Delegate.create (this, drawValues) , this.timeElapsed);
			//Anchors
			this.config.intervals.anchors = setInterval (Delegate.create (this, drawAnchors) , this.timeElapsed);
			//Draw vertical div lines
			this.config.intervals.vLine = setInterval (Delegate.create (this, drawVLines) , this.timeElapsed);
			//Update timer
			this.timeElapsed += (this.params.animation) ? this.styleM.getMaxAnimationTime (this.objects.ANCHORS, this.objects.VLINES, this.objects.DATAVALUES) : 0;
			//Dispatch event that the chart has loaded.
			this.config.intervals.renderedEvent = setInterval(Delegate.create(this, exposeChartRendered) , this.timeElapsed);
			//Render context menu
			//We do not put context menu interval as we need the menu to appear
			//right from start of the play.
			this.setContextMenu ();
		}
	}
	/**
	* returnDataAsObject method creates an object out of the parameters
	* passed to this method. The idea is that we store each data point
	* as an object with multiple (flexible) properties. So, we do not
	* use a predefined class structure. Instead we use a generic object.
	*	@param	value		Value for the data.
	*	@param	color		Hex Color code.
	*	@param	alpha		Alpha of the line
	*	@param	toolText	Tool tip text (if specified).
	*	@param	link		Link (if any) for the data.
	*	@param	showValue	Flag to show/hide value for this data.
	*	@param	isDashed	Flag whether the line would be dashed.
	*	@param	anchorSides				Number of sides of the anchor.
	*	@param	anchorRadius			Radius of the anchor (in pixels).
	*	@param	anchorBorderColor		Border color of the anchor.
	*	@param	anchorBorderThickness	Border thickness of the anchor.
	*	@param	anchorBgColor			Background color of the anchor.
	*	@param	anchorAlpha				Alpha of the anchor.
	*	@param	anchorBgAlpha			Background (fill) alpha of the anchor.
	*	@return			An object encapsulating all these properies.
	*/
	private function returnDataAsObject (value : Number, color : String, alpha : Number, toolText : String, link : String, showValue : Number, isDashed : Boolean, anchorSides : Number, anchorRadius : Number, anchorBorderColor : String, anchorBorderThickness : Number, anchorBgColor : String, anchorAlpha : Number, anchorBgAlpha : Number) : Object 
	{
		//Create a container
		var dataObj : Object = new Object ();
		//Store the values
		dataObj.value = value;
		dataObj.color = color;
		dataObj.alpha = alpha;
		dataObj.toolText = toolText;
		dataObj.link = link;
		dataObj.showValue = (showValue == 1) ? true : false;
		dataObj.dashed = isDashed;
		//Anchor properties
		dataObj.anchorSides = anchorSides;
		dataObj.anchorRadius = anchorRadius;
		dataObj.anchorBorderColor = anchorBorderColor;
		dataObj.anchorBorderThickness = anchorBorderThickness;
		dataObj.anchorBgColor = anchorBgColor;
		dataObj.anchorAlpha = anchorAlpha;
		dataObj.anchorBgAlpha = anchorBgAlpha;
		//If the given number is not a valid number or it's missing
		//set appropriate flags for this data point
		dataObj.isDefined = ((dataObj.alpha == 0) || isNaN (value)) ? false : true;
		//Other parameters
		//X & Y Position of data point
		dataObj.x = 0;
		dataObj.y = 0;
		//X & Y Position of value tb
		dataObj.valTBX = 0;
		dataObj.valTBY = 0;
		//TextField Object
		dataObj.valueTf;
		//Return the container
		return dataObj;
	}
	/**
	* returnDataAsCat method returns data of a <category> element as
	* an object
	*	@param	label		Label of the category.
	*	@param	showLabel	Whether to show the label of this category.
	*	@param	toolText	Tool-text for the category
	*	@return			A container object with the given properties
	*/
	private function returnDataAsCat (label : String, showLabel : Number, toolText : String) : Object
	{
		//Create container object
		var catObj : Object = new Object ();
		catObj.label = label;
		catObj.showLabel = ((showLabel == 1) && (label != undefined) && (label != null) && (label != "")) ? true : false;
		catObj.toolText = toolText;
		//X and Y Position
		catObj.x = 0;
		catObj.y = 0;
		//Return container
		return catObj;
	}
	/**
	* parseXML method parses the XML data, sets defaults and validates
	* the attributes before storing them to data storage objects.
	*/
	private function parseXML () : Void 
	{
		//Get the element nodes
		var arrDocElement : Array = this.xmlData.childNodes;
		//Loop variable
		var i : Number;
		var j : Number;
		var k : Number;
		var l : Number;
		//Look for <graph> element
		for (i = 0; i < arrDocElement.length; i ++)
		{
			//If it's a <graph> element, proceed.
			//Do case in-sensitive mathcing by changing to upper case
			if (arrDocElement [i].nodeName.toUpperCase () == "GRAPH" || arrDocElement [i].nodeName.toUpperCase () == "CHART")
			{
				//Extract attributes of <graph> element
				this.parseAttributes (arrDocElement [i]);
				//Now, get the child nodes - first level nodes
				//Level 1 nodes can be - CATEGORIES, DATASET, STYLES etc.
				var arrLevel1Nodes : Array = arrDocElement [i].childNodes;
				var setNode : XMLNode;
				//Iterate through all level 1 nodes.
				for (j = 0; j < arrLevel1Nodes.length; j ++)
				{
					if (arrLevel1Nodes [j].nodeName.toUpperCase () == "CATEGORIES")
					{
						//Categories Node.
						var categoriesNode : XMLNode = arrLevel1Nodes [j];
						//Convert attributes to array
						var categoriesAtt : Array = this.getAttributesArray (categoriesNode);
						//Extract attributes of this node.
						this.params.catFont = getFV (categoriesAtt ["font"] , this.params.outCnvBaseFont);
						this.params.catFontSize = getFN (categoriesAtt ["fontsize"] , this.params.outCnvBaseFontSize);
						this.params.catFontColor = formatColor (getFV (categoriesAtt ["fontcolor"] , this.params.outCnvBaseFontColor));
						//Get reference to child node.
						var arrLevel2Nodes : Array = arrLevel1Nodes [j].childNodes;
						//Iterate through all child-nodes of CATEGORIES element
						//and search for CATEGORY or VLINE node
						for (k = 0; k < arrLevel2Nodes.length; k ++)
						{
							if (arrLevel2Nodes [k].nodeName.toUpperCase () == "CATEGORY")
							{
								//Category Node.
								//Update counter
								this.num ++;
								//Extract attributes
								var categoryNode : XMLNode = arrLevel2Nodes [k];
								var categoryAtt : Array = this.getAttributesArray (categoryNode);
								//Category label.
								var catLabel : String = getFV (categoryAtt ["label"] , categoryAtt ["name"] , "");
								var catShowLabel : Number = getFN (categoryAtt ["showlabel"] , categoryAtt ["showname"] , this.params.showLabels);
								var catToolText : String = getFV (categoryAtt ["tooltext"] , categoryAtt ["hovertext"] , catLabel);
								//Store it in data container.
								this.categories [this.num] = this.returnDataAsCat (catLabel, catShowLabel, catToolText);
							} 
							else if (arrLevel2Nodes [k].nodeName.toUpperCase () == "VLINE")
							{
								//Vertical axis division Node - extract child nodes
								var vLineNode : XMLNode = arrLevel2Nodes [k];
								//Parse and store
								this.parseVLineNode (vLineNode, this.num);
							}
						}
					} else if (arrLevel1Nodes [j].nodeName.toUpperCase () == "AXIS")
					{
						//Increment
						this.numAxis ++;
						//Axis Node.
						var axisNode : XMLNode = arrLevel1Nodes[j];
						//Convert attributes to array
						var axisAtt : Array = this.getAttributesArray (axisNode);
						//Create storage object in dataset array
						this.axis[this.numAxis] = new Object ();
						//User specified y min and max
						this.axis[this.numAxis].yAxisMaxValue = axisAtt["maxvalue"];
						this.axis[this.numAxis].yAxisMinValue = axisAtt["minvalue"];
						//Adaptive yMin - if set to true, the y min will be based on the values
						//provided. It won't be set to 0 in case of all positive values
						this.axis[this.numAxis].setAdaptiveYMin = toBoolean (getFN (axisAtt["setadaptiveymin"] , this.params.setAdaptiveYMin));						
						//Extract attributes of this node.
						this.axis[this.numAxis].color = formatColor (getFV (axisAtt["color"], this.defColors.getColor()));
						this.axis[this.numAxis].title = getFV (axisAtt["title"], " ");
						//Title position for axis - options are bottom, left, right
						this.axis[this.numAxis].titlePos = getFV (axisAtt["titlepos"], "bottom");
						//If there is any title placed at bottom then we subtract the maxHeight Value from the canvas
						this.config.paddingReqd = (this.axis[this.numAxis].titlePos.toUpperCase() == "BOTTOM")?(true):(this.config.paddingReqd);
						//Whether to show the axis on left hand side of canvas.
						this.axis[this.numAxis].axisOnLeft = toBoolean(getFN (axisAtt["axisonleft"], 1));
						this.axis[this.numAxis].axisLineThickness = getFN (axisAtt["axislinethickness"], 2);
						//Whether it's a visible axis or imaginary axis.
						this.axis[this.numAxis].showAxis = toBoolean(getFN (axisAtt["showaxis"], 1));
						this.axis[this.numAxis].tickWidth = getFN (axisAtt["tickwidth"], 2);
						//Line Properties - for plot
						this.axis[this.numAxis].lineThickness = getFN (axisAtt["linethickness"], this.params.lineThickness);
						this.axis[this.numAxis].lineColor = formatColor (getFV (axisAtt["linecolor"], this.params.lineColor));
						this.axis[this.numAxis].lineAlpha = getFN (axisAtt["linealpha"], this.params.lineAlpha );						
						//Plot is dashed
						this.axis[this.numAxis].lineDashed = toBoolean(getFN (axisAtt["linedashed"], this.params.lineDashed));
						//Dash Properties
						this.axis[this.numAxis].lineDashLen = getFN (axisAtt["linedashlen"], this.params.lineDashLen);
						this.axis[this.numAxis].lineDashGap = getFN (axisAtt["linedashgap"], this.params.lineDashGap );						
						//Zero plane properties
						this.axis[this.numAxis].showZeroPlane = toBoolean (getFN (axisAtt["showzeroplane"] , this.params.showZeroPlane));
						this.axis[this.numAxis].zeroPlaneColor = formatColor (getFV (axisAtt["zeroplanecolor"] , this.axis[this.numAxis].color));
						this.axis[this.numAxis].zeroPlaneThickness = getFN (axisAtt["zeroplanethickness"] , this.params.zeroPlaneThickness);
						this.axis[this.numAxis].zeroPlaneAlpha = getFN (axisAtt["zeroplanealpha"] , this.params.zeroPlaneAlpha);
						//Necessarily need a default value for numDivLines.
						this.axis[this.numAxis].numDivLines = getFN (axisAtt["numdivlines"] , this.params.numDivLines);
						this.axis[this.numAxis].divLineColor = formatColor (getFV (axisAtt["divlinecolor"] , this.axis[this.numAxis].color, this.params.divLineColor));
						this.axis[this.numAxis].divLineThickness = getFN (axisAtt["divlinethickness"] , this.params.divLineThickness);
						this.axis[this.numAxis].divLineAlpha = getFN (axisAtt["divlinealpha"] , this.params.divLineAlpha);
						this.axis[this.numAxis].divLineIsDashed = toBoolean (getFN (axisAtt["divlineisdashed"] , this.params.divLineIsDashed));
						this.axis[this.numAxis].divLineDashLen = getFN (axisAtt["divlinedashlen"] , this.params.divLineDashLen);
						this.axis[this.numAxis].divLineDashGap = getFN (axisAtt["divlinedashgap"] , this.params.divLineDashGap);
						//Value properties
						this.axis[this.numAxis].showYAxisValues = getFN(axisAtt ["showyaxisvalues"] , axisAtt["showyaxisvalue"] , 1);
						this.axis[this.numAxis].showDivLineValues = toBoolean (getFN (axisAtt["showdivlinevalue"] , axisAtt["showdivlinevalues"] , this.axis[this.numAxis].showYAxisValues));
						this.axis[this.numAxis].showLimits = toBoolean (getFN (axisAtt["showlimits"] , axisAtt ["showlimits"] , this.axis[this.numAxis].showYAxisValues));						
						//Y-axis value step- i.e., show all y-axis or skip every x(th) value
						this.axis[this.numAxis].yAxisValuesStep = int (getFN (axisAtt["yaxisvaluesstep"] , axisAtt["yaxisvaluestep"] , this.params.yAxisValuesStep));
						//Cannot be less than 1
						this.axis[this.numAxis].yAxisValuesStep = (this.axis[this.numAxis].yAxisValuesStep < 1) ? 1 : this.axis[this.numAxis].yAxisValuesStep;
						//Whether to automatically adjust div lines
						this.axis[this.numAxis].adjustDiv = toBoolean (getFN (axisAtt["adjustdiv"] , this.params.adjustDiv));
						//Option whether the format the number (using Commas)
						this.axis[this.numAxis].formatNumber = toBoolean (getFN (axisAtt["formatnumber"] , this.params.formatNumber));
						//Option to format number scale
						this.axis[this.numAxis].formatNumberScale = toBoolean (getFN (axisAtt["formatnumberscale"] , this.params.formatNumberScale));
						//Number Scales
						this.axis[this.numAxis].defaultNumberScale = getFV (axisAtt["defaultnumberscale"] , this.params.defaultNumberScale);
						this.axis[this.numAxis].numberScaleUnit = getFV (axisAtt["numberscaleunit"] , this.params.numberScaleUnit);
						this.axis[this.numAxis].numberScaleValue = getFV (axisAtt["numberscalevalue"] , this.params.numberScaleValue);
						//Number prefix and suffix
						this.axis[this.numAxis].numberPrefix = getFV (axisAtt["numberprefix"] , this.params.numberPrefix);
						this.axis[this.numAxis].numberSuffix = getFV (axisAtt["numbersuffix"] , this.params.numberSuffix);
						//Decimal Precision (number of decimal places to be rounded to)
						this.axis[this.numAxis].decimals = getFV (axisAtt ["decimals"] , axisAtt ["decimalprecision"], this.params.decimals);
						//Force Decimal Padding
						this.axis[this.numAxis].forceDecimals = toBoolean (getFV (axisAtt ["forcedecimals"] , this.params.forceDecimals ));
						//y-Axis values decimals
						this.axis[this.numAxis].yAxisValueDecimals = getFV (axisAtt ["yaxisvaluedecimals"] , axisAtt ["yaxisvaluesdecimals"] , axisAtt ["divlinedecimalprecision"] , axisAtt ["limitsdecimalprecision"], this.params.yAxisValueDecimals);						
						
						//Create dataset array under it.
						this.axis[this.numAxis].dataset  = new Array ();
						//Get reference to child node.
						var arrLevel2Nodes : Array = arrLevel1Nodes [j].childNodes;
						//Iterate through all child-nodes of AXIS element
						//and search for DATASET node
						//Counter
						var setNumDs:Number = 0;
						for (k = 0; k < arrLevel2Nodes.length; k ++)
						{
							if (arrLevel2Nodes [k].nodeName.toUpperCase () == "DATASET")
							{
								//Increment
								setNumDs++;
								//Dataset node.
								var dataSetNode : XMLNode = arrLevel2Nodes [k];
								//Get attributes array
								var dsAtts : Array = this.getAttributesArray (dataSetNode);
								//Create storage object in dataset array
								this.axis[this.numAxis].dataset [setNumDs] = new Object ();
								//Store attributes
								this.axis[this.numAxis].dataset [setNumDs].seriesName = getFV (dsAtts ["seriesname"] , "");
								this.axis[this.numAxis].dataset [setNumDs].color = formatColor (getFV (dsAtts ["color"] , this.axis[this.numAxis].lineColor, this.axis[this.numAxis].color));
								this.axis[this.numAxis].dataset [setNumDs].alpha = getFN (dsAtts ["alpha"] , this.axis[this.numAxis].lineAlpha);
								this.axis[this.numAxis].dataset [setNumDs].showValues = toBoolean (getFN (dsAtts ["showvalues"] , this.params.showValues));
								this.axis[this.numAxis].dataset [setNumDs].includeInLegend = toBoolean (getFN (dsAtts ["includeinlegend"] , 1));
								//Data set line properties
								this.axis[this.numAxis].dataset [setNumDs].lineDashed = toBoolean (getFN (dsAtts ["dashed"] , this.axis[this.numAxis].lineDashed));
								//Dash Properties
								this.axis[this.numAxis].dataset [setNumDs].lineDashLen = getFN (dsAtts ["linedashlen"] , this.axis[this.numAxis].lineDashLen);
								this.axis[this.numAxis].dataset [setNumDs].lineDashGap = getFN (dsAtts ["linedashgap"] , this.axis[this.numAxis].lineDashGap);
								this.axis[this.numAxis].dataset [setNumDs].lineThickness = getFN (dsAtts ["linethickness"] , this.axis[this.numAxis].lineThickness);
								//Data set anchors
								this.axis[this.numAxis].dataset [setNumDs].drawAnchors = toBoolean (getFN (dsAtts ["drawanchors"] , dsAtts ["showanchors"] , (this.params.drawAnchors) ?1 : 0));
								this.axis[this.numAxis].dataset [setNumDs].anchorSides = getFN (dsAtts ["anchorsides"] , this.params.anchorSides);
								this.axis[this.numAxis].dataset [setNumDs].anchorRadius = getFN (dsAtts ["anchorradius"] , this.params.anchorRadius);
								this.axis[this.numAxis].dataset [setNumDs].anchorBorderColor = formatColor (getFV (dsAtts ["anchorbordercolor"] , this.params.anchorBorderColor, this.axis[numAxis].dataset [setNumDs].color));
								this.axis[this.numAxis].dataset [setNumDs].anchorBorderThickness = getFN (dsAtts ["anchorborderthickness"] , this.params.anchorBorderThickness);
								this.axis[this.numAxis].dataset [setNumDs].anchorBgColor = formatColor (getFV (dsAtts ["anchorbgcolor"] , this.params.anchorBgColor));
								this.axis[this.numAxis].dataset [setNumDs].anchorAlpha = getFN (dsAtts ["anchoralpha"] , this.params.anchorAlpha);
								this.axis[this.numAxis].dataset [setNumDs].anchorBgAlpha = getFN (dsAtts ["anchorbgalpha"] , this.params.anchorBgAlpha);
								//Create data array under it.
								this.axis[this.numAxis].dataset [setNumDs].data = new Array ();
								//Get reference to child node.
								var arrLevel3Nodes : Array = arrLevel2Nodes [k].childNodes;
								//Iterate through all child-nodes of DATASET element
								//and search for SET node
								//Counter
								var setCount : Number = 0;
								for (l = 0; l < arrLevel3Nodes.length; l ++)
								{
									if (arrLevel3Nodes [l].nodeName.toUpperCase () == "SET")
									{
										//Set Node. So extract the data.
										//Update counter
										setCount ++;
										//Get reference to node.
										setNode = arrLevel3Nodes [l];
										//Get attributes
										var atts : Array;
										atts = this.getAttributesArray (setNode);
										//Now, get value.
										var setValue : Number = this.getSetValue (atts ["value"]);
										//We do NOT unescape the link, as this will be done
										//in invokeLink method for the links that user clicks.
										var setLink : String = getFV (atts ["link"] , "");
										var setToolText : String = getFV (atts ["tooltext"] , atts ["hovertext"]);
										var setColor : String = formatColor (getFV (atts ["color"] , this.axis[this.numAxis].dataset [setNumDs].color));
										var setAlpha : Number = getFN (atts ["alpha"] , this.axis[this.numAxis].dataset [setNumDs].alpha);
										var setShowValue : Number = getFN (atts ["showvalue"] , this.axis[this.numAxis].dataset [setNumDs].showValues);
										var setDashed : Boolean = toBoolean (getFN (atts ["dashed"] , this.axis[this.numAxis].dataset [setNumDs].lineDashed));
										//Anchor properties for individual set
										var setAnchorSides : Number = getFN (atts ["anchorsides"] , this.axis[this.numAxis].dataset [setNumDs].anchorSides);
										var setAnchorRadius : Number = getFN (atts ["anchorradius"] , this.axis[this.numAxis].dataset [setNumDs].anchorRadius);
										var setAnchorBorderColor : String = formatColor (getFV (atts ["anchorbordercolor"] , this.axis[this.numAxis].dataset [setNumDs].anchorBorderColor));
										var setAnchorBorderThickness : Number = getFN (atts ["anchorborderthickness"] , this.axis[this.numAxis].dataset [setNumDs].anchorBorderThickness);
										var setAnchorBgColor : String = formatColor (getFV (atts ["anchorbgcolor"] , this.axis[this.numAxis].dataset [setNumDs].anchorBgColor));
										var setAnchorAlpha : Number = getFN (atts ["anchoralpha"] , this.axis[this.numAxis].dataset [setNumDs].anchorAlpha);
										var setAnchorBgAlpha : Number = getFN (atts ["anchorbgalpha"] , this.axis[this.numAxis].dataset [setNumDs].anchorBgAlpha);
										//Store all these attributes as object.
										this.axis[this.numAxis].dataset [setNumDs].data [setCount] = this.returnDataAsObject (setValue, setColor, setAlpha, setToolText, setLink, setShowValue, setDashed, setAnchorSides, setAnchorRadius, setAnchorBorderColor, setAnchorBorderThickness, setAnchorBgColor, setAnchorAlpha, setAnchorBgAlpha);
									}
								}
								this.axis[this.numAxis].dataset[setNumDs].num = setCount;
							}
						}
						this.axis[this.numAxis].numDS = setNumDs;
						this.maxDS = (this.maxDS<setNumDs)?(setNumDs):(this.maxDS);
					} else if (arrLevel1Nodes [j].nodeName.toUpperCase () == "STYLES")
					{
						//Styles Node - extract child nodes
						var arrStyleNodes : Array = arrLevel1Nodes [j].childNodes;
						//Parse the style nodes to extract style information
						super.parseStyleXML (arrStyleNodes);
					} 
				}
			}
		}
		//Delete all temporary objects used for parsing XML Data document
		delete setNode;
		delete arrDocElement;
		delete arrLevel1Nodes;
		delete arrLevel2Nodes;
		delete arrLevel3Nodes;
	}
	/**
	* parseAttributes method parses the attributes and stores them in
	* chart storage objects.
	* Starting ActionScript 2, the parsing of XML attributes have also
	* become case-sensitive. However, prior versions of FusionCharts
	* supported case-insensitive attributes. So we need to parse all
	* attributes as case-insensitive to maintain backward compatibility.
	* To do so, we first extract all attributes from XML, convert it into
	* lower case and then store it in an array. Later, we extract value from
	* this array.
	* @param	graphElement	XML Node containing the <graph> element
	*							and it's attributes
	*/
	private function parseAttributes (graphElement : XMLNode) : Void 
	{
		//Array to store the attributes
		var atts : Array = this.getAttributesArray (graphElement);
		//NOW IT'S VERY NECCESARY THAT WHEN WE REFERENCE THIS ARRAY
		//TO GET AN ATTRIBUTE VALUE, WE SHOULD PROVIDE THE ATTRIBUTE
		//NAME IN LOWER CASE. ELSE, UNDEFINED VALUE WOULD SHOW UP.
		//Extract attributes pertinent to this chart
		//Which palette to use?
		this.params.palette = getFN (atts ["palette"] , 1);
		//Whether to show about FusionCharts Menu Item - by default set to on
		this.params.showFCMenuItem = toBoolean(getFN (atts ["showfcmenuitem"] , 1));
		// ---------- PADDING AND SPACING RELATED ATTRIBUTES ----------- //
		//captionPadding = Space between caption/subcaption and canvas start Y
		this.params.captionPadding = getFN (atts ["captionpadding"] , 10);
		//Canvas Padding is the space between the canvas left/right border
		//and first/last data point
		this.params.canvasPadding = getFN (atts ["canvaspadding"] , 20);
		//Padding for x-axis name - to the right
		this.params.xAxisNamePadding = getFN (atts ["xaxisnamepadding"] , 5);
		//Label padding - Vertical space between the labels and canvas end position
		this.params.labelPadding = getFN (atts ["labelpadding"] , atts ["labelspadding"] , 3);
		//Value padding - vertical space between the anchors and start of value textboxes
		this.params.valuePadding = getFN (atts ["valuepadding"] , 2);
		//Padding of legend from right/bottom side of canvas
		this.params.legendPadding = getFN (atts ["legendpadding"] , 6);
		//Chart Margins - Empty space at the 4 sides
		this.params.chartLeftMargin = getFN (atts ["chartleftmargin"] , 15);
		this.params.chartRightMargin = getFN (atts ["chartrightmargin"] , 15);
		this.params.chartTopMargin = getFN (atts ["charttopmargin"] , 15);
		this.params.chartBottomMargin = getFN (atts ["chartbottommargin"] , 15);
		// -------------------------- HEADERS ------------------------- //
		//Chart Caption and sub Caption
		this.params.caption = getFV (atts ["caption"] , "");
		this.params.subCaption = getFV (atts ["subcaption"] , "");
		//X and Y Axis Name
		this.params.xAxisName = getFV (atts ["xaxisname"] , "");
		//Adaptive yMin - if set to true, the y min will be based on the values
		//provided. It won't be set to 0 in case of all positive values
		this.params.setAdaptiveYMin = toBoolean (getFN (atts ["setadaptiveymin"] , 0));
		// --------------------- CONFIGURATION ------------------------- //
		//Whether to set animation for entire chart.
		this.params.animation = toBoolean (getFN (atts ["animation"] , 1));
		//Axis Shift
		this.params.allowAxisShift = toBoolean (getFN (atts ["allowaxisshift"] , 1));
		//Whether to set the default chart animation
		this.params.defaultAnimation = toBoolean (getFN (atts ["defaultanimation"] , 1));
		//Whether null data points are to be connected or left broken
		this.params.connectNullData = toBoolean (getFN (atts ["connectnulldata"] , 0));
		//Configuration to set whether to show the labels
		this.params.showLabels = toBoolean (getFN (atts ["showlabels"] , atts ["shownames"] , 1));
		//Label Display Mode - WRAP, STAGGER, ROTATE or NONE
		this.params.labelDisplay = getFV (atts ["labeldisplay"] , "WRAP");
		//Remove spaces and capitalize
		this.params.labelDisplay = StringExt.removeSpaces (this.params.labelDisplay);
		this.params.labelDisplay = this.params.labelDisplay.toUpperCase ();
		//Option to show vertical x-axis labels
		this.params.rotateLabels = getFV (atts ["rotatelabels"] , atts ["rotatenames"]);
		//Whether to slant label (if rotated)
		this.params.slantLabels = toBoolean (getFN (atts ["slantlabels"] , atts ["slantlabel"] , 0));
		//Angle of rotation based on slanting
		this.config.labelAngle = (this.params.slantLabels == true) ? 315 : 270;
		//If rotateLabels has been explicitly specified, we assign ROTATE value to this.params.labelDisplay
		this.params.labelDisplay = (this.params.rotateLabels == "1") ? "ROTATE" : this.params.labelDisplay;
		//Step value for labels - i.e., show all labels or skip every x label
		this.params.labelStep = int (getFN (atts ["labelstep"] , 1));
		//Cannot be less than 1
		this.params.labelStep = (this.params.labelStep < 1) ? 1 : this.params.labelStep;
		//Number of stagger lines
		this.params.staggerLines = int (getFN (atts ["staggerlines"] , 2));
		//Cannot be less than 2
		this.params.staggerLines = (this.params.staggerLines < 2) ? 2 : this.params.staggerLines;
		//Configuration whether to show data values
		this.params.showValues = toBoolean (getFN (atts ["showvalues"] , 1));
		//Whether to rotate values
		this.params.rotateValues = toBoolean (getFN (atts ["rotatevalues"] , 0));
		//Option to show/hide y-axis values
		this.params.showYAxisValues = getFN (atts ["showyaxisvalues"] , atts ["showyaxisvalue"] , 1);
		this.params.showLimits = toBoolean (getFN (atts ["showlimits"] , this.params.showYAxisValues));
		this.params.showDivLineValues = toBoolean (getFN (atts ["showdivlinevalue"] , atts ["showdivlinevalues"] , this.params.showYAxisValues));
		//Y-axis value step- i.e., show all y-axis or skip every x(th) value
		this.params.yAxisValuesStep = int (getFN (atts ["yaxisvaluesstep"] , atts ["yaxisvaluestep"] , 1));
		//Cannot be less than 1
		this.params.yAxisValuesStep = (this.params.yAxisValuesStep < 1) ? 1 : this.params.yAxisValuesStep;
		//Whether to automatically adjust div lines
		this.params.adjustDiv = toBoolean (getFN (atts ["adjustdiv"] , 1));
		//Click URL
		this.params.clickURL = getFV (atts ["clickurl"] , "");
		// ------------------------- COSMETICS -----------------------------//
		//CheckBox properties
		this.params.allowSelection = toBoolean (getFN (atts ["allowselection"] , 1));
		this.params.showTitle = toBoolean (getFN (atts ["showtitle"] , 1));
		this.params.checkBoxColor = getFV (atts ["checkboxcolor"] , this.defColors.get2DBgColor (this.params.palette));
		//Background properties - Gradient
		this.params.bgColor = getFV (atts ["bgcolor"] , this.defColors.get2DBgColor (this.params.palette));
		this.params.bgAlpha = getFV (atts ["bgalpha"] , this.defColors.get2DBgAlpha (this.params.palette));
		this.params.bgRatio = getFV (atts ["bgratio"] , this.defColors.get2DBgRatio (this.params.palette));
		this.params.bgAngle = getFV (atts ["bgangle"] , this.defColors.get2DBgAngle (this.params.palette));
		//Border Properties of chart
		this.params.showBorder = toBoolean (getFN (atts ["showborder"] , 1));
		this.params.borderColor = formatColor (getFV (atts ["bordercolor"] , this.defColors.get2DBorderColor (this.params.palette)));
		this.params.borderThickness = getFN (atts ["borderthickness"] , 1);
		this.params.borderAlpha = getFN (atts ["borderalpha"] , this.defColors.get2DBorderAlpha (this.params.palette));
		//Background swf
		this.params.bgSWF = getFV (atts ["bgswf"] , "");
		this.params.bgSWFAlpha = getFN (atts ["bgswfalpha"] , 100);
		//Canvas background properties - Gradient
		this.params.canvasBgColor = getFV (atts ["canvasbgcolor"] , this.defColors.get2DCanvasBgColor (this.params.palette));
		this.params.canvasBgAlpha = getFV (atts ["canvasbgalpha"] , this.defColors.get2DCanvasBgAlpha (this.params.palette));
		this.params.canvasBgRatio = getFV (atts ["canvasbgratio"] , this.defColors.get2DCanvasBgRatio (this.params.palette));
		this.params.canvasBgAngle = getFV (atts ["canvasbgangle"] , this.defColors.get2DCanvasBgAngle (this.params.palette));
		//Canvas Border properties
		this.params.canvasBorderColor = formatColor (getFV (atts ["canvasbordercolor"] , this.defColors.get2DCanvasBorderColor (this.params.palette)));
		this.params.canvasBorderThickness = getFN (atts ["canvasborderthickness"] , 2);
		this.params.canvasBorderAlpha = getFN (atts ["canvasborderalpha"] , this.defColors.get2DCanvasBorderAlpha (this.params.palette));
		//Line Properties
		this.params.lineColor = atts ["linecolor"];
		this.params.lineThickness = getFN (atts ["linethickness"] , 2);
		this.params.lineAlpha = getFN (atts ["linealpha"] , 100);
		//Plot is dashed
		this.params.lineDashed = toBoolean (getFN (atts ["linedashed"] , 0));
		//Dash Properties
		this.params.lineDashLen = getFN (atts ["linedashlen"] , 5);
		this.params.lineDashGap = getFN (atts ["linedashgap"] , 4);
		//Legend properties
		this.params.showLegend = toBoolean (getFN (atts ["showlegend"] , 1));
		//Alignment position
		this.params.legendPosition = getFV (atts ["legendposition"] , "BOTTOM");
		//Legend position can be either RIGHT or BOTTOM -Check for it
		this.params.legendPosition = (this.params.legendPosition.toUpperCase () == "RIGHT") ?"RIGHT" : "BOTTOM";
		this.params.legendCaption = getFV(atts ["legendcaption"] , "");
		this.params.legendMarkerCircle = toBoolean(getFN(atts ["legendmarkercircle"] , 0));
		this.params.legendBorderColor = formatColor (getFV (atts ["legendbordercolor"] , this.defColors.get2DLegendBorderColor (this.params.palette)));
		this.params.legendBorderThickness = getFN (atts ["legendborderthickness"] , 1);
		this.params.legendBorderAlpha = getFN (atts ["legendborderalpha"] , 100);
		this.params.legendBgColor = getFV (atts ["legendbgcolor"] , this.defColors.get2DLegendBgColor (this.params.palette));
		this.params.legendBgAlpha = getFN (atts ["legendbgalpha"] , 100);
		this.params.legendShadow = toBoolean (getFN (atts ["legendshadow"] , 1));
		this.params.legendAllowDrag = toBoolean (getFN (atts ["legendallowdrag"] , 0));
		this.params.legendScrollBgColor = formatColor (getFV (atts ["legendscrollbgcolor"] , "CCCCCC"));
		this.params.legendScrollBarColor = formatColor (getFV (atts ["legendscrollbarcolor"] , this.params.legendBorderColor));
		this.params.legendScrollBtnColor = formatColor (getFV (atts ["legendscrollbtncolor"] , this.params.legendBorderColor));
		//Horizontal grid division Lines - Number, color, thickness & alpha
		//Necessarily need a default value for numDivLines.
		this.params.numDivLines = getFN (atts ["numdivlines"] , 4);
		this.params.divLineThickness = getFN (atts ["divlinethickness"] , 1);
		this.params.divLineAlpha = getFN (atts ["divlinealpha"] , this.defColors.get2DDivLineAlpha (this.params.palette));
		this.params.divLineIsDashed = toBoolean (getFN (atts ["divlineisdashed"] , 0));
		this.params.divLineDashLen = getFN (atts ["divlinedashlen"] , 4);
		this.params.divLineDashGap = getFN (atts ["divlinedashgap"] , 2);
		//Vertical div lines
		this.params.numVDivLines = getFN (atts ["numvdivlines"] , 0);
		this.params.vDivLineColor = formatColor (getFV (atts ["vdivlinecolor"] , this.params.divLineColor));
		this.params.vDivLineThickness = getFN (atts ["vdivlinethickness"] , this.params.divLineThickness);
		this.params.vDivLineAlpha = getFN (atts ["vdivlinealpha"] , this.params.divLineAlpha);
		this.params.vDivLineIsDashed = toBoolean (getFN (atts ["vdivlineisdashed"] , this.params.divLineIsDashed));
		this.params.vDivLineDashLen = getFN (atts ["vdivlinedashlen"] , this.params.divLineDashLen);
		this.params.vDivLineDashGap = getFN (atts ["vdivlinedashgap"] , this.params.divLineDashGap);
		//Zero Plane properties
		this.params.showZeroPlane = toBoolean (getFN (atts ["showzeroplane"] , 1));
		this.params.zeroPlaneThickness = getFN (atts ["zeroplanethickness"] , 1);
		this.params.zeroPlaneAlpha = getFN (atts ["zeroplanealpha"] , this.params.divLineAlpha);
		//Alternating grid colors
		this.params.showAlternateVGridColor = toBoolean (getFN (atts ["showalternatevgridcolor"] , 0));
		this.params.alternateVGridColor = formatColor (getFV (atts ["alternatevgridcolor"] , this.defColors.get2DAltVGridColor (this.params.palette)));
		this.params.alternateVGridAlpha = getFN (atts ["alternatevgridalpha"] , this.defColors.get2DAltVGridAlpha (this.params.palette));
		//Shadow properties
		this.params.showShadow = toBoolean (getFN (atts ["showshadow"] , 1));
		//Anchor Properties
		this.params.drawAnchors = toBoolean (getFN (atts ["drawanchors"] , atts ["showanchors"] , 1));
		this.params.anchorSides = getFN (atts ["anchorsides"] , 10);
		this.params.anchorRadius = getFN (atts ["anchorradius"] , 3);
		this.params.anchorBorderColor = atts ["anchorbordercolor"];
		this.params.anchorBorderThickness = getFN (atts ["anchorborderthickness"] , 1);
		this.params.anchorBgColor = formatColor (getFV (atts ["anchorbgcolor"] , this.defColors.get2DAnchorBgColor (this.params.palette)));
		this.params.anchorAlpha = getFN (atts ["anchoralpha"] , 100);
		this.params.anchorBgAlpha = getFN (atts ["anchorbgalpha"] , this.params.anchorAlpha);
		//Tool Tip - Show/Hide, Background Color, Border Color, Separator Character
		this.params.showToolTip = toBoolean (getFN (atts ["showtooltip"] , atts ["showhovercap"] , 1));
		this.params.seriesNameInToolTip = toBoolean (getFN (atts ["seriesnameintooltip"] , 1));
		this.params.toolTipBgColor = formatColor (getFV (atts ["tooltipbgcolor"] , atts ["hovercapbgcolor"] , atts ["hovercapbg"] , this.defColors.get2DToolTipBgColor (this.params.palette)));
		this.params.toolTipBorderColor = formatColor (getFV (atts ["tooltipbordercolor"] , atts ["hovercapbordercolor"] , atts ["hovercapborder"] , this.defColors.get2DToolTipBorderColor (this.params.palette)));
		this.params.toolTipSepChar = getFV (atts ["tooltipsepchar"] , atts ["hovercapsepchar"] , ", ");
		//Font Properties
		this.params.baseFont = getFV (atts ["basefont"] , "Verdana");
		this.params.baseFontSize = getFN (atts ["basefontsize"] , 10);
		this.params.baseFontColor = formatColor (getFV (atts ["basefontcolor"] , this.defColors.get2DBaseFontColor (this.params.palette)));
		this.params.outCnvBaseFont = getFV (atts ["outcnvbasefont"] , this.params.baseFont);
		this.params.outCnvBaseFontSize = getFN (atts ["outcnvbasefontsize"] , this.params.baseFontSize);
		this.params.outCnvBaseFontColor = formatColor (getFV (atts ["outcnvbasefontcolor"] , this.params.baseFontColor));
		// ------------------------- NUMBER FORMATTING ---------------------------- //
		//Option whether the format the number (using Commas)
		this.params.formatNumber = toBoolean (getFN (atts ["formatnumber"] , 1));
		//Option to format number scale
		this.params.formatNumberScale = toBoolean (getFN (atts ["formatnumberscale"] , 1));
		//Number Scales
		this.params.defaultNumberScale = getFV (atts ["defaultnumberscale"] , "");
		this.params.numberScaleUnit = getFV (atts ["numberscaleunit"] , "K,M");
		this.params.numberScaleValue = getFV (atts ["numberscalevalue"] , "1000,1000");
		//Number prefix and suffix
		this.params.numberPrefix = getFV (atts ["numberprefix"] , "");
		this.params.numberSuffix = getFV (atts ["numbersuffix"] , "");
		//Decimal Separator Character
		this.params.decimalSeparator = getFV (atts ["decimalseparator"] , ".");
		//Thousand Separator Character
		this.params.thousandSeparator = getFV (atts ["thousandseparator"] , ",");
		//Input decimal separator and thousand separator. In some european countries,
		//commas are used as decimal separators and dots as thousand separators. In XML,
		//if the user specifies such values, it will give a error while converting to
		//number. So, we accept the input decimal and thousand separator from user, so that
		//we can covert it accordingly into the required format.
		this.params.inDecimalSeparator = getFV (atts ["indecimalseparator"] , "");
		this.params.inThousandSeparator = getFV (atts ["inthousandseparator"] , "");
		//Decimal Precision (number of decimal places to be rounded to)
		this.params.decimals = getFV (atts ["decimals"] , atts ["decimalprecision"]);
		//Force Decimal Padding
		this.params.forceDecimals = toBoolean (getFN (atts ["forcedecimals"] , 0));
		//y-Axis values decimals
		this.params.yAxisValueDecimals = getFV (atts ["yaxisvaluedecimals"] , atts ["yaxisvaluesdecimals"] , atts ["divlinedecimalprecision"] , atts ["limitsdecimalprecision"]);
		// ---------------------- Image Saving ------------------------------//
		this.params.imageSave = toBoolean (getFN (atts ["imagesave"] , 0));
		this.params.imageSaveURL = getFV (atts ["imagesaveurl"] , "");
		this.params.imageSaveDialogColor = formatColor (getFV (atts ["imagesavedialogcolor"] , "E2E2E2"));
		this.params.imageSaveDialogFontColor = formatColor (getFV (atts ["imagesavedialogfontcolor"] , "666666"));
	}
	/**
	* getMaxDataValue method gets the maximum y-axis data value present
	* in the data for a particular axis.
	*	@return	The maximum value present in the data provided.
	*/
	private function getMaxDataValue (axisIndex:Number) : Number 
	{
		var maxValue : Number;
		var firstNumberFound : Boolean = false;
		var j : Number, k:Number;
		for (j = 1; j <= this.axis[axisIndex].numDS; j ++)
		{
			for (k = 1; k <= this.axis[axisIndex].dataset[j].num; k ++)
			{
			
				//By default assume the first non-null number to be maximum
				if (firstNumberFound == false)
				{
					if (this.axis[axisIndex].dataset[j].data[k].isDefined == true)
					{
						//Set the flag that "We've found first non-null number".
						firstNumberFound = true;
						//Also assume this value to be maximum.
						maxValue = this.axis[axisIndex].dataset[j].data[k].value;
					}
				} else 
				{
					//If the first number has been found and the current data is defined, compare
					if (this.axis[axisIndex].dataset [j].data [k].isDefined)
					{
						//Store the greater number
						maxValue = (this.axis[axisIndex].dataset [j].data [k].value > maxValue) ? this.axis[axisIndex].dataset [j].data [k].value : maxValue;
					}
				}
			}
		}
		return maxValue;
	}
	/**
	* getMinDataValue method gets the minimum y-axis data value present
	* in the data for a particular axis.
	*	@reurns		The minimum value present in data
	*/
	private function getMinDataValue (axisIndex:Number) : Number 
	{
		var minValue : Number;
		var firstNumberFound : Boolean = false;
		var j:Number, k:Number;
		for (j = 1; j <= this.axis[axisIndex].numDS; j ++)
		{
			for (k = 1; k <= this.axis[axisIndex].dataset[j].num; k ++)
			{			
				//By default assume the first non-null number to be minimum
				if (firstNumberFound == false)
				{
					if (this.axis[axisIndex].dataset [j].data [k].isDefined == true)
					{
						//Set the flag that "We've found first non-null number".
						firstNumberFound = true;
						//Also assume this value to be minimum.
						minValue = this.axis[axisIndex].dataset [j].data [k].value;
					}
				} else 
				{
					//If the first number has been found and the current data is defined, compare
					if (this.axis[axisIndex].dataset [j].data [k].isDefined)
					{
						//Store the lesser number
						minValue = (this.axis[axisIndex].dataset [j].data [k].value < minValue) ? this.axis[axisIndex].dataset [j].data [k].value : minValue;
					}
				}
			}
		}
		return minValue;
	}
	/**
	* setStyleDefaults method sets the default values for styles or
	* extracts information from the attributes and stores them into
	* style objects.
	*/
	private function setStyleDefaults () : Void 
	{
		//Default font object for Caption
		//-----------------------------------------------------------------//
		var captionFont = new StyleObject ();
		captionFont.name = "_SdCaptionFont";
		captionFont.align = "center";
		captionFont.valign = "top";
		captionFont.bold = "1";
		captionFont.font = this.params.outCnvBaseFont;
		captionFont.size = this.params.outCnvBaseFontSize;
		captionFont.color = this.params.outCnvBaseFontColor;
		//Over-ride
		this.styleM.overrideStyle (this.objects.CAPTION, captionFont, this.styleM.TYPE.FONT, null);
		delete captionFont;
		//-----------------------------------------------------------------//
		//Default font object for SubCaption
		//-----------------------------------------------------------------//
		var subCaptionFont = new StyleObject ();
		subCaptionFont.name = "_SdSubCaptionFont";
		subCaptionFont.align = "center";
		subCaptionFont.valign = "top";
		subCaptionFont.bold = "1";
		subCaptionFont.font = this.params.outCnvBaseFont;
		subCaptionFont.size = this.params.outCnvBaseFontSize;
		subCaptionFont.color = this.params.outCnvBaseFontColor;
		//Over-ride
		this.styleM.overrideStyle (this.objects.SUBCAPTION, subCaptionFont, this.styleM.TYPE.FONT, null);
		delete subCaptionFont;
		//-----------------------------------------------------------------//
		//Default font object for XAxisName
		//-----------------------------------------------------------------//
		var xAxisNameFont = new StyleObject ();
		xAxisNameFont.name = "_SdXAxisNameFont";
		xAxisNameFont.align = "center";
		xAxisNameFont.valign = "middle";
		xAxisNameFont.bold = "1";
		xAxisNameFont.font = this.params.outCnvBaseFont;
		xAxisNameFont.size = this.params.outCnvBaseFontSize;
		xAxisNameFont.color = this.params.outCnvBaseFontColor;
		//Over-ride
		this.styleM.overrideStyle (this.objects.XAXISNAME, xAxisNameFont, this.styleM.TYPE.FONT, null);
		delete xAxisNameFont;
		//-----------------------------------------------------------------//
		//Default font object for yAxisValues
		//-----------------------------------------------------------------//
		var yAxisValuesFont = new StyleObject ();
		yAxisValuesFont.name = "_SdYAxisValuesFont";
		yAxisValuesFont.align = "right";
		yAxisValuesFont.valign = "middle";
		yAxisValuesFont.font = this.params.outCnvBaseFont;
		yAxisValuesFont.size = this.params.outCnvBaseFontSize;
		yAxisValuesFont.color = this.params.outCnvBaseFontColor;
		//Over-ride
		this.styleM.overrideStyle (this.objects.YAXISVALUES, yAxisValuesFont, this.styleM.TYPE.FONT, null);
		delete yAxisValuesFont;
		//-----------------------------------------------------------------//
		//Default font object for Axis Title
		//-----------------------------------------------------------------//
		var titleFont = new StyleObject ();
		titleFont.name = "_SdTitleFont";
		titleFont.align = "center";
		titleFont.valign = "bottom";
		titleFont.bold = "1";
		titleFont.font = this.params.outCnvBaseFont;
		titleFont.size = this.params.outCnvBaseFontSize;
		titleFont.color = this.params.outCnvBaseFontColor;
		//Over-ride
		this.styleM.overrideStyle (this.objects.AXISTITLE, titleFont, this.styleM.TYPE.FONT, null);
		delete titleFont;
		//-----------------------------------------------------------------//
		//Default font object for DataLabels
		//-----------------------------------------------------------------//
		var dataLabelsFont = new StyleObject ();
		dataLabelsFont.name = "_SdDataLabelsFont";
		dataLabelsFont.align = "center";
		dataLabelsFont.valign = "bottom";
		dataLabelsFont.font = this.params.catFont;
		dataLabelsFont.size = this.params.catFontSize;
		dataLabelsFont.color = this.params.catFontColor;
		//Over-ride
		this.styleM.overrideStyle (this.objects.DATALABELS, dataLabelsFont, this.styleM.TYPE.FONT, null);
		delete dataLabelsFont;
		//-----------------------------------------------------------------//
		//Default font object for Legend
		//-----------------------------------------------------------------//
		var legendFont = new StyleObject ();
		legendFont.name = "_SdLegendFont";
		legendFont.font = this.params.outCnvBaseFont;
		legendFont.size = this.params.outCnvBaseFontSize;
		legendFont.color = this.params.outCnvBaseFontColor;
		legendFont.ishtml = 1;
		legendFont.leftmargin = 3;
		//Over-ride
		this.styleM.overrideStyle (this.objects.LEGEND, legendFont, this.styleM.TYPE.FONT, null);
		delete legendFont;
		//-----------------------------------------------------------------//
		//Default font object for DataValues
		//-----------------------------------------------------------------//
		var dataValuesFont = new StyleObject ();
		dataValuesFont.name = "_SdDataValuesFont";
		dataValuesFont.align = "center";
		dataValuesFont.valign = "middle";
		dataValuesFont.font = this.params.baseFont;
		dataValuesFont.size = this.params.baseFontSize;
		dataValuesFont.color = this.params.baseFontColor;
		//Over-ride
		this.styleM.overrideStyle (this.objects.DATAVALUES, dataValuesFont, this.styleM.TYPE.FONT, null);
		delete dataValuesFont;
		//-----------------------------------------------------------------//
		//Default font object for ToolTip
		//-----------------------------------------------------------------//
		var toolTipFont = new StyleObject ();
		toolTipFont.name = "_SdToolTipFont";
		toolTipFont.font = this.params.baseFont;
		toolTipFont.size = this.params.baseFontSize;
		toolTipFont.color = this.params.baseFontColor;
		toolTipFont.bgcolor = this.params.toolTipBgColor;
		toolTipFont.bordercolor = this.params.toolTipBorderColor;
		//Over-ride
		this.styleM.overrideStyle (this.objects.TOOLTIP, toolTipFont, this.styleM.TYPE.FONT, null);
		delete toolTipFont;
		//-----------------------------------------------------------------//
		//Default Effect (Shadow) object for DataPlot
		//-----------------------------------------------------------------//
		if (this.params.showShadow)
		{
			var dataPlotShadow = new StyleObject ();
			dataPlotShadow.name = "_SdDataPlotShadow";
			dataPlotShadow.angle = 45;
			//Over-ride
			this.styleM.overrideStyle (this.objects.DATAPLOT, dataPlotShadow, this.styleM.TYPE.SHADOW, null);
			delete dataPlotShadow;
		}
		//-----------------------------------------------------------------//
		//Default Effect (Shadow) object for Legend
		//-----------------------------------------------------------------//
		if (this.params.legendShadow)
		{
			var legendShadow = new StyleObject ();
			legendShadow.name = "_SdLegendShadow";
			legendShadow.distance = 2;
			legendShadow.alpha = 90;
			legendShadow.angle = 45;
			//Over-ride
			this.styleM.overrideStyle (this.objects.LEGEND, legendShadow, this.styleM.TYPE.SHADOW, null);
			delete legendShadow;
		}
		//-----------------------------------------------------------------//
		//Default Animation object for DataPlot (if required)
		//-----------------------------------------------------------------//
		if (this.params.defaultAnimation)
		{
			//We need three animation objects.
			//1. XScale for data plot
			var dataPlotAnim = new StyleObject ();
			dataPlotAnim.name = "_SdDataPlotAnimXScale";
			dataPlotAnim.param = "_xscale";
			dataPlotAnim.easing = "regular";
			dataPlotAnim.wait = 0;
			dataPlotAnim.start = 0;
			dataPlotAnim.duration = 0.7;
			//Over-ride
			this.styleM.overrideStyle (this.objects.DATAPLOT, dataPlotAnim, this.styleM.TYPE.ANIMATION, "_xscale");
			delete dataPlotAnim;
			//2. YScale for data plot
			var dataPlotAnimY = new StyleObject ();
			dataPlotAnimY.name = "_SdDataPlotAnimYScale";
			dataPlotAnimY.param = "_yscale";
			dataPlotAnimY.easing = "regular";
			dataPlotAnimY.wait = 0.7;
			dataPlotAnimY.start = 0.1;
			dataPlotAnimY.duration = 0.7;
			//Over-ride
			this.styleM.overrideStyle (this.objects.DATAPLOT, dataPlotAnimY, this.styleM.TYPE.ANIMATION, "_yscale");
			delete dataPlotAnimY;
			//3. Alpha effect for anchors
			var anchorsAnim = new StyleObject ();
			anchorsAnim.name = "_SdDataAnchorAnim";
			anchorsAnim.param = "_alpha";
			anchorsAnim.easing = "regular";
			anchorsAnim.wait = 0;
			anchorsAnim.start = 0;
			anchorsAnim.duration = 0.5;
			//Over-ride
			this.styleM.overrideStyle (this.objects.ANCHORS, anchorsAnim, this.styleM.TYPE.ANIMATION, "_alpha");
			delete anchorsAnim;
		}
		//-----------------------------------------------------------------//
		
	}
	/**
	* calcVLinesPos method calculates the x position for the various
	* vLines defined. Also, it validates them.
	*/
	private function calcVLinesPos ()
	{
		var i : Number;
		//Iterate through all the vLines		
		for (i = 1; i <= numVLines; i ++)
		{
			//If the vLine is after 1st data and before last data
			if (this.vLines [i].index > 0 && this.vLines [i].index < this.num)
			{
				//Set it's x position
				this.vLines [i].x = this.categories [this.vLines [i].index].x + (this.categories [this.vLines [i].index + 1].x - this.categories [this.vLines [i].index].x) / 2;
			} else 
			{
				//Invalidate it
				this.vLines [i].isValid = false;
			}
		}
	}
	/**
	* This function over-rides the detectNumberScales method of Chart class.
	* detectNumberScales method detects whether we've been provided
	* with number scales for the axis. If yes, we parse them. This method needs to
	* called before calculatePoints, as calculatePoint methods calls
	* formatNumber, which in turn uses number scales.
	* Here, we also adjust the numberPrefix and numberSuffix if they're
	* still present in encoded form. Like, if the user has specified encoded
	* numberPrefix and Suffix in dataURL mode, it will show the direct value.
	* Therefore, we need to change them into proper characters.
	* Finally, we set the proper number of decimals for the chart based on the
	* decimal input.
	*	@return	Nothing.
	*/
	private function detectNumberScales () : Void
	{
		var i:Number;
		var j:Number;
		for (i = 1; i <= this.numAxis; i ++)
		{
			//Check if either has been defined
			if (this.axis[i].numberScaleValue.length == 0 || this.axis[i].numberScaleUnit.length == 0 || this.axis[i].formatNumberScale == false)
			{
				//Set flag to false
				this.axis[i].numberScaleDefined = false;
			} else 
			{
				//Set flag to true
				this.axis[i].numberScaleDefined = true;
				//Split the data into arrays
				this.axis[i].nsv = new Array ();
				this.axis[i].nsu = new Array ();
				//Parse the number scale value
				this.axis[i].nsv = this.axis[i].numberScaleValue.split (",");
				//Convert all number scale values to numbers as they're
				//currently in string format.
				var j : Number;
				for (j = 0; j < this.axis[i].nsv.length; j++)
				{
					this.axis[i].nsv [j] = Number (this.axis[i].nsv [j]);
					//If any of numbers are NaN, set defined to false
					if (isNaN (this.axis[i].nsv [j]))
					{
						this.axis[i].numberScaleDefined = false;
					}
				}
				//Parse the number scale unit
				this.axis[i].nsu = this.axis[i].numberScaleUnit.split (",");
				//If the length of two arrays do not match, set defined to false.
				if (this.axis[i].nsu.length != this.axis[i].nsv.length)
				{
					this.axis[i].numberScaleDefined = false;
				}
			}
			//Convert numberPrefix and numberSuffix now.
			this.axis[i].numberPrefix = this.unescapeChar (this.axis[i].numberPrefix);
			this.axis[i].numberSuffix = this.unescapeChar (this.axis[i].numberSuffix);
			//Always keep to a decimal precision of minimum 2 if the number
			//scale is defined, as we've just checked for decimal precision of numbers
			//and not the numbers against number scale. So, even if they do not need yield a
			//decimal, we keep 2, as we do not force decimals on numbers.
			if (this.axis[i].numberScaleDefined == true)
			{
				maxDecimals = (maxDecimals > 2) ?maxDecimals : 2;
			}
			//Get proper value for decimals
			this.axis[i].decimals = Number (getFV (this.axis[i].decimals, maxDecimals));
			//Decimal Precision cannot be less than 0 - so adjust it
			if (this.axis[i].decimals < 0)
			{
				this.axis[i].decimals = 0;
			}
			//Get proper value for yAxisValueDecimals
			this.axis[i].yAxisValueDecimals = Number (getFV (this.axis[i].yAxisValueDecimals, this.axis[i].decimals));
		}
	}
	/**
	* calculatePoints method calculates the various points on the chart.
	*/
	private function calculatePoints ()
	{
		//Loop variable
		var i : Number;
		var j : Number;
		var k : Number;
		//Feed empty data - By default there should be equal number of <categories>
		//and <set> element within each dataset. If in case, <set> elements fall short,
		//we need to append empty data at the end.
		for (i = 1; i <= this.numAxis; i ++)
		{
			for (j = 1; j <= this.axis[i].numDS; j ++)
			{
				for (k = 1; k <= this.num;k++)
				{
					if (this.axis[i].dataset [j].data [k] == undefined)
					{
						this.axis[i].dataset [j].data [k] = this.returnDataAsObject(NaN);
					}
				}
			}
		}
		//Format all the numbers on the chart and store their display values
		//We format and store here itself, so that later, whenever needed,
		//we just access displayValue instead of formatting once again.
		//Also set tool tip text values
		var toolText : String
		for (i = 1; i <= this.numAxis; i ++)
		{
			for (j = 1; j <= this.axis[i].numDS; j ++)
			{
				for (k = 1; k <= this.axis[i].dataset[j].num; k ++)
				{
					//Format and store
					this.axis[i].dataset [j].data [k].displayValue = NumberFormatting.formatNumber (this.axis[i].dataset [j].data [k].value, this.axis[i].formatNumber, this.axis[i].decimals, this.axis[i].forceDecimals, this.axis[i].formatNumberScale, this.axis[i].defaultNumberScale, this.axis[i].nsv, this.axis[i].nsu, this.axis[i].numberPrefix, this.axis[i].numberSuffix, this.params.decimalSeparator, this.params.thousandSeparator, this.axis[i].numberScaleDefined);
					//Tool tip text.
					//Preferential Order - Set Tool Text (No concatenation) > SeriesName + Cat Name + Value
					if (this.axis[i].dataset [j].data [k].toolText == undefined || this.axis[i].dataset [j].data [k].toolText == "")
					{
						//If the tool tip text is not already defined
						//If labels have been defined
						toolText = (this.params.seriesNameInToolTip && this.axis[i].dataset [j].seriesName != "") ? (this.axis[i].dataset[j].seriesName + this.params.toolTipSepChar) : "";
						toolText = toolText + ((this.categories[k].toolText != "") ? (this.categories[k].toolText + this.params.toolTipSepChar) : "");
						toolText = toolText + this.axis[i].dataset [j].data [k].displayValue;
						this.axis[i].dataset [j].data [k].toolText = toolText;
					}
				}
			}
		}
		//Based on label step, set showLabel of each data point as required.
		//Visible label count
		var visibleCount : Number = 0;
		var finalVisibleCount : Number = 0;
		for (i = 1; i <= this.num; i ++)
		{
			//Now, the label can be preset to be hidden (set via XML)
			if (this.categories [i].showLabel)
			{
				visibleCount ++;
				//If label step is defined, we need to set showLabel of those
				//labels which fall on step as false.
				if ((i - 1) % this.params.labelStep == 0)
				{
					this.categories [i].showLabel = true;
				} else 
				{
					this.categories [i].showLabel = false;
				}
			}
			//Update counter
			finalVisibleCount = (this.categories [i].showLabel) ? (finalVisibleCount + 1) : (finalVisibleCount);
		}
		//Set canvas startX
		var canvasStartX : Number = this.params.chartLeftMargin;
		//Now, we need to calculate the available Height on the canvas.
		//Available height = total Chart height minus the list below
		// - Chart Top and Bottom Margins
		// - Space for Caption, Sub Caption and caption padding
		// - Height of data labels
		// - xAxisName
		// - Legend (If to be shown at bottom position)
		//Initialize canvasHeight to total height minus margins
		var canvasHeight : Number = this.height - (this.params.chartTopMargin + this.params.chartBottomMargin);
		//Reduce the height if the titlepos of the axis is to be placed at the bottom
		if(this.config.paddingReqd == true) {
			canvasHeight -= this.MAX_TITLE_HEIGHT;
		}
		//Max width
		var canvasWidth : Number = this.width - (this.params.chartLeftMargin + this.params.chartRightMargin);
		//Set canvasStartY
		var canvasStartY : Number = this.params.chartTopMargin;
		//Now, if we've to show caption
		if (this.params.caption != "")
		{
			//Create text field to get height
			var captionObj : Object = createText (true, this.params.caption, this.tfTestMC, 1, testTFX, testTFY, 0, this.styleM.getTextStyle (this.objects.CAPTION) , false, 0, 0);
			//Store the height
			canvasStartY = canvasStartY + captionObj.height;
			canvasHeight = canvasHeight - captionObj.height;
			//Create element for caption - to store width & height
			this.elements.caption = returnDataAsElement (0, 0, captionObj.width, captionObj.height);
			delete captionObj;
		}
		//Now, if we've to show sub-caption
		if (this.params.subCaption != "")
		{
			//Create text field to get height
			var subCaptionObj : Object = createText (true, this.params.subCaption, this.tfTestMC, 1, testTFX, testTFY, 0, this.styleM.getTextStyle (this.objects.SUBCAPTION) , false, 0, 0);
			//Store the height
			canvasStartY = canvasStartY + subCaptionObj.height;
			canvasHeight = canvasHeight - subCaptionObj.height;
			//Create element for sub caption - to store height
			this.elements.subCaption = returnDataAsElement (0, 0, subCaptionObj.width, subCaptionObj.height);
			delete subCaptionObj;
		}
		//Now, if either caption or sub-caption was shown, we also need to adjust caption padding
		if (this.params.caption != "" || this.params.subCaption != "")
		{
			//Account for padding
			canvasStartY = canvasStartY + this.params.captionPadding;
			canvasHeight = canvasHeight - this.params.captionPadding;
		}
		//Now, if data labels are to be shown, we need to account for their heights
		//Data labels can be rendered in 3 ways:
		//1. Normal - no staggering - no wrapping - no rotation
		//2. Wrapped - no staggering - no rotation
		//3. Staggered - no wrapping - no rotation
		//4. Rotated - no staggering - no wrapping
		//Placeholder to store max height
		this.config.maxLabelHeight = 0;
		this.config.labelAreaHeight = 0;
		var labelObj : Object;
		var labelStyleObj : Object = this.styleM.getTextStyle (this.objects.DATALABELS);
		if (this.params.labelDisplay == "ROTATE")
		{
			//Case 4: If the labels are rotated, we iterate through all the string labels
			//provided to us and get the height and store max.
			for (i = 1; i <= this.num; i ++)
			{
				//If the label is to be shown
				if (this.categories [i].showLabel)
				{
					//Set style bold/italic to false and font to embedded font
					labelStyleObj.font = _embeddedFont;
					labelStyleObj.bold = false;
					labelStyleObj.italic = false;
					//Create text box and get height
					labelObj = createText (true, this.categories [i].label, this.tfTestMC, 1, testTFX, testTFY, this.config.labelAngle, labelStyleObj, false, 0, 0);
					//Store the larger
					this.config.maxLabelHeight = (labelObj.height > this.config.maxLabelHeight) ? (labelObj.height) : (this.config.maxLabelHeight);
				}
			}
			//Store max label height as label area height.
			this.config.labelAreaHeight = this.config.maxLabelHeight;
		} else if (this.params.labelDisplay == "WRAP")
		{
			//Case 2 (WRAP): Create all the labels on the chart. Set width as
			//totalAvailableWidth/finalVisibleCount.
			//Set max height as 50% of available canvas height at this point of time. Find all
			//and select the max one.
			var maxLabelWidth : Number = (canvasWidth / finalVisibleCount);
			var maxLabelHeight : Number = (canvasHeight / 2);
			//Store it in config for later usage
			this.config.wrapLabelWidth = maxLabelWidth;
			this.config.wrapLabelHeight = maxLabelHeight;
			for (i = 1; i <= this.num; i ++)
			{
				//If the label is to be shown
				if (this.categories [i].showLabel)
				{
					//Create text box and get height
					labelObj = createText (true, this.categories [i].label, this.tfTestMC, 1, testTFX, testTFY, 0, labelStyleObj, true, maxLabelWidth, maxLabelHeight);
					//Store the larger
					this.config.maxLabelHeight = (labelObj.height > this.config.maxLabelHeight) ? (labelObj.height) : (this.config.maxLabelHeight);
				}
			}
			//Store max label height as label area height.
			this.config.labelAreaHeight = this.config.maxLabelHeight;
		} else 
		{
			//Case 1,3: Normal or Staggered Label
			//We iterate through all the labels, and if any of them has &lt or < (HTML marker)
			//embedded in them, we add them to the array, as for them, we'll need to individually
			//create and see the text height. Also, the first element in the array - we set as
			//ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz1234567890_=....
			//Create array to store labels.
			var strLabels : Array = new Array ();
			strLabels.push ("ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz1234567890_=/*-+~`");
			//Now, iterate through all the labels and for those visible labels, whcih have < sign,
			//add it to array.
			for (i = 1; i <= this.num; i ++)
			{
				//If the label is to be shown
				if (this.categories [i].showLabel)
				{
					if ((this.categories [i].label.indexOf ("&lt;") > - 1) || (this.categories [i].label.indexOf ("<") > - 1))
					{
						strLabels.push (this.categories [i].label);
					}
				}
			}
			//Now, we've the array for which we've to check height (for each element).
			for (i = 0; i < strLabels.length; i ++)
			{
				//Create text box and get height
				labelObj = createText (true, this.categories [i].label, this.tfTestMC, 1, testTFX, testTFY, 0, labelStyleObj, false, 0, 0);
				//Store the larger
				this.config.maxLabelHeight = (labelObj.height > this.config.maxLabelHeight) ? (labelObj.height) : (this.config.maxLabelHeight);
			}
			//We now have the max label height. If it's staggered, then store accordingly, else
			//simple mode
			if (this.params.labelDisplay == "STAGGER")
			{
				//Multiply max label height by stagger lines.
				this.config.labelAreaHeight = this.params.staggerLines * this.config.maxLabelHeight;
			} else 
			{
				this.config.labelAreaHeight = this.config.maxLabelHeight;
			}
		}
		if (this.config.labelAreaHeight > 0)
		{
			//Deduct the calculated label height from canvas height
			canvasHeight = canvasHeight - this.config.labelAreaHeight - this.params.labelPadding;
		}
		//Delete objects
		delete labelObj;
		delete labelStyleObj;
		//Accomodate space for xAxisName (if to be shown);
		if (this.params.xAxisName != "")
		{
			//Create text field to get height
			var xAxisNameObj : Object = createText (true, this.params.xAxisName, this.tfTestMC, 1, testTFX, testTFY, 0, this.styleM.getTextStyle (this.objects.XAXISNAME) , false, 0, 0);
			//Store the height
			canvasHeight = canvasHeight - xAxisNameObj.height - this.params.xAxisNamePadding;
			//Object to store width and height of xAxisName
			this.elements.xAxisName = returnDataAsElement (0, 0, xAxisNameObj.width, xAxisNameObj.height);
			delete xAxisNameObj;
		}
		//We have canvas start Y and canvas height
		//We now need to calculate the available Width on the canvas.
		//Available width = total Chart width minus the list below
		// - Left and Right Margin
		// - Axis
		// - AxisValues
		// - Legend (If to be shown at right)
		//We first check whether the legend is to be drawn
		if (this.params.showLegend)
		{
			//Object to store dimensions
			var lgndDim : Object;
			//Create container movie clip for legend
			this.lgndMC = this.cMC.createEmptyMovieClip ("Legend", this.dm.getDepth ("LEGEND"));
			//Create instance of legend
			if (this.params.legendPosition == "BOTTOM")
			{
				//Maximum Height - 50% of stage
				lgnd = new Legend (lgndMC, this.styleM.getTextStyle (this.objects.LEGEND) , this.params.legendPosition, canvasStartX + canvasWidth / 2, this.height / 2, canvasWidth, (this.height - (this.params.chartTopMargin + this.params.chartBottomMargin)) * 0.5, this.params.legendAllowDrag, this.width, this.height, this.params.legendBgColor, this.params.legendBgAlpha, this.params.legendBorderColor, this.params.legendBorderThickness, this.params.legendBorderAlpha, this.params.legendScrollBgColor, this.params.legendScrollBarColor, this.params.legendScrollBtnColor);
			} 
			else 
			{
				//Maximum Width - 40% of stage
				lgnd = new Legend (lgndMC, this.styleM.getTextStyle (this.objects.LEGEND) , this.params.legendPosition, this.width / 2, canvasStartY + canvasHeight / 2, (this.width - (this.params.chartLeftMargin + this.params.chartRightMargin)) * 0.4, canvasHeight, this.params.legendAllowDrag, this.width, this.height, this.params.legendBgColor, this.params.legendBgAlpha, this.params.legendBorderColor, this.params.legendBorderThickness, this.params.legendBorderAlpha, this.params.legendScrollBgColor, this.params.legendScrollBarColor, this.params.legendScrollBtnColor);
			}
			//Feed data set series Name for legend
			for (i = 1; i <= this.numAxis; i ++)
			{
				for (j = 1; j <= this.axis[i].numDS; j ++)	{
					if (this.axis[i].dataset [j].includeInLegend && this.axis[i].dataset [j].seriesName != "")
					{
						lgnd.addItem (this.axis[i].dataset[j].seriesName, this.axis[i].dataset [j].color);
					}
				}
			}
			//If user has defined a caption for the legend, set it
			if (this.params.legendCaption!=""){
				lgnd.setCaption(this.params.legendCaption);
			}
			//Whether to use circular marker
			lgnd.useCircleMarker(this.params.legendMarkerCircle);
			if (this.params.legendPosition == "BOTTOM")
			{
				lgndDim = lgnd.getDimensions ();
				//Now deduct the height from the calculated canvas height
				canvasHeight = canvasHeight - lgndDim.height - this.params.legendPadding;
				//Re-set the legend position
				this.lgnd.resetXY (canvasStartX + canvasWidth / 2, this.height - this.params.chartBottomMargin - lgndDim.height / 2 );
			}
			else
			{
				//Get dimensions
				lgndDim = lgnd.getDimensions ();
				//Now deduct the width from the calculated canvas width
				canvasWidth = canvasWidth - lgndDim.width - this.params.legendPadding;
				//Right position
				this.lgnd.resetXY (this.width - this.params.chartRightMargin - lgndDim.width / 2, canvasStartY + canvasHeight / 2);
			}
		}
		//Assign 80% of the total canvas width for the axis
		var axisMaxWidth:Number = 0.8*canvasWidth;
		//Max Height is same as the avaialable Canvas height
		var axisMaxHeight:Number = canvasHeight;
		//Set the first time axis log flag - so that we just log the information once
		var firstTimeAxisLog:Boolean = true;
		var totalWidth:Number = 0;
		// Variables
		var leftAxisXPos:Number = canvasStartX, lastLeftAxisWidth:Number = 0;
		var rightAxisXPos:Number = 0, lastRightAxisWidth:Number = 0;
		//Get the depth for AXIS and DIVLINES
		var depth:Number = this.dm.getDepth("AXIS");
		var depthDiv:Number = this.dm.getDepth("DIVLINES");
		//Now scan through the Axis list
		for (i = 1; i <= this.numAxis; i ++, depth++, depthDiv++)
		{
			//If Axis is not imaginary
			if(this.axis[i].showAxis) {
				this.checkListener.onCheck = Delegate.create(this, onCheck);
				//Only if the AXIS are allowed to shift
				if(this.params.allowAxisShift) {
					this.clickListener.onAxisClick = Delegate.create(this, onAxisClick);
				}
				//If Axis has to be placed on left of canvas
				if(this.axis[i].axisOnLeft) {
					//Increase the left Axis counter
					this.numAxisOnLeft++;
					//Add it to the leftAxisArray
					this.leftAxisArray.push(i);
					//Increase the X position for the next left Axis if there is any
					leftAxisXPos += (lastLeftAxisWidth+ this.AXIS_PADDING);
				} else  {
					//Increase the right Axis counter
					this.numAxisOnRight++;
					//Add it to the rightAxisArray
					this.rightAxisArray.push(i);
					//Increase the X position for the next right Axis if there is any
					rightAxisXPos += (lastRightAxisWidth+ this.AXIS_PADDING);
				}
				//Create an Object to store all the zero plane properties
				var zeroPlaneProp:Object = new Object();
				//Zero plane properties
				zeroPlaneProp.showZeroPlane = this.axis[i].showZeroPlane;
				zeroPlaneProp.zeroPlaneColor = this.axis[i].zeroPlaneColor; 
				zeroPlaneProp.zeroPlaneThickness = this.axis[i].zeroPlaneThickness;
				zeroPlaneProp.zeroPlaneAlpha= this.axis[i].zeroPlaneAlpha;
				//Create an Object to store all the divLine properties
				var divLineProp:Object = new Object();
				//DivLine Properties
				divLineProp.showDivLineValues = this.axis[i].showDivLineValues;
				divLineProp.divLineThickness = this.axis[i].divLineThickness;
				divLineProp.divLineAlpha= this.axis[i].divLineAlpha;
				divLineProp.divLineIsDashed= this.axis[i].divLineIsDashed;
				divLineProp.divLineDashLen= this.axis[i].divLineDashLen;
				divLineProp.divLineDashGap= this.axis[i].divLineDashGap;
				divLineProp.divLineColor = this.axis[i].divLineColor;
				//DivLine Font Object to store fontObject
				var divLineFontObj: Object  = this.styleM.getTextStyle (this.objects.YAXISVALUES);
				divLineProp.divLineFontObj= divLineFontObj;
				//Create an Object to store all the number formatting properties
				var numberFormattingProp:Object = new Object();
				//Number formatting properties
				numberFormattingProp.formatNumber = this.axis[i].formatNumber ;
				numberFormattingProp.formatNumberScale = this.axis[i].formatNumberScale;
				numberFormattingProp.defaultNumberScale= this.axis[i].defaultNumberScale;
				numberFormattingProp.nsu = this.axis[i].nsu;
				numberFormattingProp.nsv = this.axis[i].nsv;
				numberFormattingProp.numberScaleDefined = this.axis[i].numberScaleDefined;
				numberFormattingProp.numberPrefix = this.axis[i].numberPrefix;
				numberFormattingProp.numberSuffix = this.axis[i].numberSuffix ;
				numberFormattingProp.decimalSeparator = this.params.decimalSeparator;
				numberFormattingProp.thousandSeparator = this.params.thousandSeparator;
				numberFormattingProp.yAxisValueDecimals = this.axis[i].yAxisValueDecimals;
				numberFormattingProp.forceDecimals = this.axis[i].forceDecimals;
				numberFormattingProp.decimals = this.axis[i].decimals;
				//Create an instance for the text style of the axis title
				var axisTitleFontObj: Object  = this.styleM.getTextStyle (this.objects.AXISTITLE);
				//Only if it is placed on left of canvas
				if(this.axis[i].axisOnLeft) {
					//Create an instance of the Vertical Axis for the left Axis
					this.vAxis[i] = new VerticalAxis(this.cMC, this.cursorMC, i, depth, depthDiv, this.axis[i].showAxis, leftAxisXPos, canvasStartY, axisMaxHeight, this.axis[i].axisOnLeft, this.axis[i].title, this.axis[i].titlePos, this.getMaxDataValue (i) , this.getMinDataValue (i) , this.axis[i].yAxisMaxValue, this.axis[i].yAxisMinValue, this.axis[i].setAdaptiveYMin, this.axis[i].adjustDiv, this.axis[i].numDivLines, divLineProp, numberFormattingProp, zeroPlaneProp, this.axis[i].showLimits, this.axis[i].yAxisValuesStep, axisTitleFontObj, this.params.allowSelection, this.params.showTitle, this.styleM, this.objects, this.macro, this.params.animation, this.params.allowAxisShift, this.params.checkBoxColor);
					//Check if shifting is allowed
					if(this.params.allowAxisShift) {
						//Add the CheckBox listener
						this.vAxis[i].addEventListener("onCheck", this.checkListener);
						this.vAxis[i].addEventListener("onAxisClick", this.clickListener);
					}
					//Set the cosmetic properties
					this.vAxis[i].setParams(this.axis[i].axisLineThickness, this.axis[i].color, this.axis[i].tickWidth);
					//Calculate the space require
					this.vAxis[i].calcPoints();
					//Increase the axis width -  so that we can have available canvas width
					lastLeftAxisWidth = this.vAxis[i].getWidth();
					//Add it to total width
					totalWidth += (lastLeftAxisWidth+this.AXIS_PADDING);
					//if the total Axis Width is below the max allocated then set it as the Axis else
					//Make it a hidden/imaginary axis
					if(totalWidth < axisMaxWidth) {
						//Set the new starting position of canvas
						canvasStartX = canvasStartX+ lastLeftAxisWidth + this.AXIS_PADDING;
					}
				} else {
					//Create an instance for the right axis
					this.vAxis[i] = new VerticalAxis(this.cMC, this.cursorMC, i, depth, depthDiv, this.axis[i].showAxis, rightAxisXPos, canvasStartY, axisMaxHeight, this.axis[i].axisOnLeft, this.axis[i].title, this.axis[i].titlePos, this.getMaxDataValue (i) , this.getMinDataValue (i) , this.axis[i].yAxisMaxValue, this.axis[i].yAxisMinValue, this.axis[i].setAdaptiveYMin, this.axis[i].adjustDiv, this.axis[i].numDivLines, divLineProp, numberFormattingProp, zeroPlaneProp, this.axis[i].showLimits, this.axis[i].yAxisValuesStep, axisTitleFontObj, this.params.allowSelection, this.params.showTitle, this.styleM, this.objects, this.macro, this.params.animation, this.params.allowAxisShift, this.params.checkBoxColor);
					//Check if shifting is permit
					if(this.params.allowAxisShift) {
						//Add the CheckBox listener
						this.vAxis[i].addEventListener("onCheck", checkListener);
						this.vAxis[i].addEventListener("onAxisClick", clickListener);
					}
					//Set the cosmetic parameters of axis
					this.vAxis[i].setParams(this.axis[i].axisLineThickness, this.axis[i].color, this.axis[i].tickWidth);
					//Calculate the space require
					this.vAxis[i].calcPoints();
					//Add it to total right width required 
					lastRightAxisWidth = this.vAxis[i].getWidth();
					//Add it to total width
					totalWidth += (lastRightAxisWidth+this.AXIS_PADDING);
				}
				//Only if the axis is coming within the alloted width limit 
				if(totalWidth < axisMaxWidth)  {
					canvasWidth -= (this.vAxis[i].getWidth()+ this.AXIS_PADDING);
				} else {
					//Hide the current axis
					this.axis[i].showAxis = false;
					//Reset this paramter in the object instance
					this.vAxis[i].hide();
					//remove it from the array
					if(this.axis[i].axisOnLeft) {
						//Decrease the counter
						this.numAxisOnLeft--;
						this.leftAxisArray.pop();
					} else {
						//Decrease the counter
						this.numAxisOnRight--;
						this.rightAxisArray.pop();
					}
					//We log this message only once
					if(firstTimeAxisLog) {
						//Add an entry to the logger
						this.log ("One or more axes were set as imaginary:", "Due to constraint for space on the chart, one or more axes have been set as imaginary, and as such, would not be visible on the chart. You may increase the width of chart to show the same.", Logger.LEVEL.ERROR);
						//Make it false so that we do not log the same message again and again
						firstTimeAxisLog = false;
					}
				}
			}
		}
		//Set the left primary Axis
		if(this.numAxisOnLeft >0) {
			//Set the last left Visible axis as the primary axis
			this.vAxis[this.leftAxisArray[this.leftAxisArray.length-1]].setPrimary(true);
		}
		//Set the Right primary Axis
		if(this.numAxisOnRight >0) {
			//Set the first visible right axis as the primary axis
			this.vAxis[this.rightAxisArray[0]].setPrimary(true);
		}
		//Create an element to represent the canvas now.
		this.elements.canvas = returnDataAsElement (canvasStartX+ this.AXIS_PADDING, canvasStartY, canvasWidth-this.AXIS_PADDING, canvasHeight);
		//We now need to calculate the position of lines on the chart.
		//Now, calculate the width between two points on chart
		var interPointWidth : Number = (this.elements.canvas.w - (2 * this.params.canvasPadding)) / (this.num - 1);
		//Now scan through the Axis list
		for (i = 1; i <= this.numAxis; i ++)
		{
			for (j = 1; j <= this.axis[i].numDS; j ++)
			{
				//Now, store the positions of the points
				for (k = 1; k <= this.num; k ++)
				{
					//X-Position
					//Now, if there is only 1 point on the chart, we center it. Else, we get even X.
					this.axis[i].dataset [j].data [k].x = (this.num == 1) ? (this.elements.canvas.x + this.elements.canvas.w / 2) : (this.elements.canvas.x + this.params.canvasPadding + (interPointWidth * (k - 1)));
					if (j == 1)
					{
						this.categories [k].x = this.axis[i].dataset [j].data [k].x;
					}
					//Set the y position
					this.axis[i].dataset [j].data [k].y = this.getAxisPosition (this.axis[i].dataset [j].data [k].value, this.vAxis[i].getMaxValue() , this.vAxis[i].getMinValue() , this.elements.canvas.y, this.elements.canvas.toY, true, 0);
					//Store value textbox y position
					this.axis[i].dataset [j].data [k].valTBY = this.axis[i].dataset [j].data [k].y;
				}
			}
		}
	}
	/**
	* allotDepths method allots the depths for various chart objects
	* to be rendered. We do this before hand, so that we can later just
	* go on rendering chart objects, without swapping.
	*/
	private function allotDepths () : Void 
	{
		//Background
		this.dm.reserveDepths ("BACKGROUND", 1);
		//Click URL Handler
		this.dm.reserveDepths ("CLICKURLHANDLER", 1);
		//Background SWF
		this.dm.reserveDepths ("BGSWF", 1);
		//Canvas
		this.dm.reserveDepths ("CANVAS", 1);
		//If vertical grid is to be shown
		if (this.params.showAlternateVGridColor)
		{
			this.dm.reserveDepths ("VGRID", Math.ceil ((this.params.numVDivLines + 1) / 2));
		}
		//Vertical Div Lines
		this.dm.reserveDepths ("VDIVLINES", this.params.numVDivLines);
		//Axis
		this.dm.reserveDepths ("AXIS", this.numAxis);
		//DivLines
		this.dm.reserveDepths ("DIVLINES", this.numAxis);
		//Caption
		this.dm.reserveDepths ("CAPTION", 1);
		//Sub-caption
		this.dm.reserveDepths ("SUBCAPTION", 1);
		//X-Axis Name
		this.dm.reserveDepths ("XAXISNAME", 1);
		//Data Labels
		this.dm.reserveDepths ("DATALABELS", this.num);
		//Line Chart
		this.dm.reserveDepths ("DATAPLOT", this.numAxis*this.maxDS);
		//Vertical div lines
		this.dm.reserveDepths ("VLINES", this.numVLines);
		//Canvas Border
		this.dm.reserveDepths ("CANVASBORDER", 1);
		//Anchors
		this.dm.reserveDepths ("ANCHORS", this.num*this.numAxis*this.maxDS);
		//Data Values
		this.dm.reserveDepths ("DATAVALUES", this.num*this.numAxis*this.maxDS);
		//Legend
		this.dm.reserveDepths ("LEGEND", 1);
		//Cursor
		this.dm.reserveDepths ("CURSOR", 1);
	}
	//--------------- VISUAL RENDERING METHODS -------------------------//
	/**
	* drawHeaders method renders the following on the chart:
	* CAPTION, SUBCAPTION, XAXISNAME
	*/
	private function drawHeaders ()
	{
		//Render caption
		if (this.params.caption != "")
		{
			var captionStyleObj : Object = this.styleM.getTextStyle (this.objects.CAPTION);
			captionStyleObj.align = "center";
			captionStyleObj.vAlign = "bottom";
			var captionObj : Object = createText (false, this.params.caption, this.cMC, this.dm.getDepth ("CAPTION") , this.elements.canvas.x + (this.elements.canvas.w / 2) , this.params.chartTopMargin, 0, captionStyleObj, false, 0, 0);
			//Apply animation
			if (this.params.animation)
			{
				this.styleM.applyAnimation (captionObj.tf, this.objects.CAPTION, this.macro, this.elements.canvas.x + (this.elements.canvas.w / 2) - (this.elements.caption.w / 2) , 0, this.params.chartTopMargin, 0, 100, null, null, null);
			}
			//Apply filters
			this.styleM.applyFilters (captionObj.tf, this.objects.CAPTION);
			//Delete
			delete captionObj;
			delete captionStyleObj;
		}
		//Render sub caption
		if (this.params.subCaption != "")
		{
			var subCaptionStyleObj : Object = this.styleM.getTextStyle (this.objects.SUBCAPTION);
			subCaptionStyleObj.align = "center";
			subCaptionStyleObj.vAlign = "top";
			var subCaptionObj : Object = createText (false, this.params.subCaption, this.cMC, this.dm.getDepth ("SUBCAPTION") , this.elements.canvas.x + (this.elements.canvas.w / 2) , this.elements.canvas.y - this.params.captionPadding, 0, subCaptionStyleObj, false, 0, 0);
			//Apply animation
			if (this.params.animation)
			{
				this.styleM.applyAnimation (subCaptionObj.tf, this.objects.SUBCAPTION, this.macro, this.elements.canvas.x + (this.elements.canvas.w / 2) - (this.elements.subCaption.w / 2) , 0, this.elements.canvas.y - this.params.captionPadding - this.elements.subCaption.h, 0, 100, null, null, null);
			}
			//Apply filters
			this.styleM.applyFilters (subCaptionObj.tf, this.objects.SUBCAPTION);
			//Delete
			delete subCaptionObj;
			delete subCaptionStyleObj;
		}
		//Render x-axis name
		if (this.params.xAxisName != "")
		{
			var xAxisNameStyleObj : Object = this.styleM.getTextStyle (this.objects.XAXISNAME);
			xAxisNameStyleObj.align = "center";
			xAxisNameStyleObj.vAlign = "bottom";
			var xAxisNameObj : Object = createText (false, this.params.xAxisName, this.cMC, this.dm.getDepth ("XAXISNAME") , this.elements.canvas.x + (this.elements.canvas.w / 2) , this.elements.canvas.toY + this.params.labelPadding + this.config.labelAreaHeight + this.params.xAxisNamePadding, 0, xAxisNameStyleObj, false, 0, 0);
			//Apply animation
			if (this.params.animation)
			{
				this.styleM.applyAnimation (xAxisNameObj.tf, this.objects.XAXISNAME, this.macro, this.elements.canvas.x + (this.elements.canvas.w / 2) - (this.elements.subCaption.w / 2) , 0, this.elements.canvas.toY + this.config.labelAreaHeight + this.params.xAxisNamePadding, 0, 100, null, null, null);
			}
			//Apply filters
			this.styleM.applyFilters (xAxisNameObj.tf, this.objects.XAXISNAME);
			//Delete
			delete xAxisNameObj;
			delete xAxisNameStyleObj;
		}
		//Clear Interval
		clearInterval (this.config.intervals.headers);
	}
	/**
	 * drawCursor method draws the reposition cursor, which will
	 * be used to re-position the axis.
	*/
	public function drawCursor():Void {
		//Create the movie clip for cursor
		this.cursorMC = this.cMC.createEmptyMovieClip("Cursor", this.dm.getDepth("CURSOR"));
		//Hide the cursor initially
		this.cursorMC._visible = false;
		//First  Draw the lines
		//Draw the cursor
		this.cursorMC.lineStyle(2, parseInt("000000", 16), 100);
		this.cursorMC.moveTo(-2, 8);
		this.cursorMC.lineTo(-2, -8);
		this.cursorMC.moveTo(2, 8);
		this.cursorMC.lineTo(2, -8);
		this.cursorMC.moveTo(2, -4);		
		this.cursorMC.lineTo(6, 0);
		this.cursorMC.lineTo(2, 4);
		this.cursorMC.lineTo(2, -4);
		this.cursorMC.moveTo(-2, -4);
		this.cursorMC.lineTo(-6, 0);
		this.cursorMC.lineTo(-2, 4);
		this.cursorMC.lineTo(-2, -4);
		//Apply the filter
		var shadowFilter:DropShadowFilter = new DropShadowFilter(3, 45, 0x666666, 0.5, 4, 4, 1, 1, false, false, false);
		this.cursorMC.filters = [shadowFilter];
	}
	/**
	* drawLabels method draws the x-axis labels based on the parameters.
	*/
	private function drawLabels ()
	{
		var labelObj : Object;
		var labelStyleObj : Object = this.styleM.getTextStyle (this.objects.DATALABELS);
		var labelYShift : Number;
		var staggerCycle : Number = 0;
		var staggerAddFn : Number = 1;
		var depth : Number = this.dm.getDepth ("DATALABELS");
		var i : Number;
		for (i = 1; i <= this.num; i ++)
		{
			//If the label is to be shown
			if (this.categories [i].showLabel)
			{
				if (this.params.labelDisplay == "ROTATE")
				{
					//Set style bold/italic to false and font to embedded font
					labelStyleObj.font = _embeddedFont;
					labelStyleObj.bold = false;
					labelStyleObj.italic = false;
					labelStyleObj.align = "center";
					labelStyleObj.vAlign = "bottom";
					//Create text box and get height
					labelObj = createText (false, this.categories [i].label, this.cMC, depth, this.categories [i].x, this.elements.canvas.toY + this.params.labelPadding, this.config.labelAngle, labelStyleObj, false, 0, 0);
				} else if (this.params.labelDisplay == "WRAP")
				{
					//Case 2 (WRAP)
					//Set alignment
					labelStyleObj.align = "center";
					labelStyleObj.vAlign = "bottom";
					labelObj = createText (false, this.categories [i].label, this.cMC, depth, this.categories [i].x, this.elements.canvas.toY + this.params.labelPadding, 0, labelStyleObj, true, this.config.wrapLabelWidth, this.config.wrapLabelHeight);
				} else if (this.params.labelDisplay == "STAGGER")
				{
					//Case 3 (Stagger)
					//Set alignment
					labelStyleObj.align = "center";
					labelStyleObj.vAlign = "bottom";
					//Need to get cyclic position for staggered textboxes
					//Matrix formed is of 2*this.params.staggerLines - 2 rows
					var pos : Number = i % (2 * this.params.staggerLines - 2);
					//Last element needs to be reset
					pos = (pos == 0) ? (2 * this.params.staggerLines - 2) : pos;
					//Cyclic iteration
					pos = (pos > this.params.staggerLines) ? (this.params.staggerLines - (pos % this.params.staggerLines)) : pos;
					//Get position to 0 base
					pos --;
					//Shift accordingly
					var labelYShift : Number = this.config.maxLabelHeight * pos;
					labelObj = createText (false, this.categories [i].label, this.cMC, depth, this.categories [i].x, this.elements.canvas.toY + this.params.labelPadding + labelYShift, 0, labelStyleObj, false, 0, 0);
				} else 
				{
					//Render normal label
					labelStyleObj.align = "center";
					labelStyleObj.vAlign = "bottom";
					labelObj = createText (false, this.categories [i].label, this.cMC, depth, this.categories [i].x, this.elements.canvas.toY + this.params.labelPadding, 0, labelStyleObj, false, 0, 0);
				}
				//Apply filter
				this.styleM.applyFilters (labelObj.tf, this.objects.DATALABELS);
				//Apply animation
				if (this.params.animation)
				{
					this.styleM.applyAnimation (labelObj.tf, this.objects.DATALABELS, this.macro, labelObj.tf._x, 0, labelObj.tf._y, 0, 100, null, null, null);
				}
				//Increase depth
				depth ++;
			}
		}
		//Clear interval
		clearInterval (this.config.intervals.labels);
	}
	/**
	* drawAxis method draws the axis on the chart  
	*/
	private function drawAxis() : Void  {
		//Now scan through the Axis list
		for (var i:Number = 1; i <= this.numAxis; i ++)
		{
			//Set the chart canvas points - to set starting position for div lines
			this.vAxis[i].setCanvasPoints(this.elements.canvas.x, this.elements.canvas.w);
			//Draw the axis
			this.vAxis[i].drawAxis();
			//Hide the divLines initially
			this.vAxis[i].hideDivLines();
		}
		//Set the divline of the last index of the leftAxisArray or the first of rightAxisArray
		if(this.leftAxisArray.length != 0) {
			this.vAxis[this.leftAxisArray[this.leftAxisArray.length-1]].showDivLines();
		} else if(this.rightAxisArray.length != 0) {
			this.vAxis[this.rightAxisArray[0]].showDivLines();
		}
		//Clear interval
		clearInterval(this.config.intervals.axis);
	}
	/**
	* drawLineChart method draws the lines on the chart
	*/
	private function drawLineChart () : Void 
	{
		/**
		* The movie clip structure for each line (dataset) would be :
		* |- Holder
		* |- |- Chart
		* We create child movie clip as we need to animate xscale
		* and y scale. So, we need to position Chart Movie clip at 0,0
		* inside holder movie clip and then readjust Holder movie clip's
		* X and Y Position as per chart's canvas.
		*/
		var m : Number, k:Number;
		var depth : Number = this.dm.getDepth ("DATAPLOT");
		for (k = 1; k <= this.numAxis; k++)
		{
			for (m = 1; m <= this.axis[k].numDS; m ++)
			{
				//Create holder movie clip
				var holderMC : MovieClip = this.cMC.createEmptyMovieClip ("ChartHolder_" + m+ "_"+k, depth);
				//Create chart movie clip inside holder movie clip
				var chartMC : MovieClip = holderMC.createEmptyMovieClip ("Chart", 1);
				//Loop variables
				var i, j;
				//Variables to store the max and min Y positions
				var maxY : Number, minY : Number;
				//Find the index of the first defined data
				//Initialize with (this.num+1) so that if no defined data is found,
				//next loop automatically terminates
				var firstIndex : Number = this.num + 1;
				//Storage container for next plot index
				var nxt : Number;
				for (i = 1; i < this.num; i ++)
				{
					if (this.axis[k].dataset [m].data [i].isDefined)
					{
						firstIndex = i;
						break;
					}
				}
				//Now, we draw the lines inside chart
				for (i = firstIndex; i < this.num; i ++)
				{
					//We continue only if this data index is defined
					if (this.axis[k].dataset [m].data [i].isDefined)
					{
						//Get next Index
						nxt = i + 1;
						//Now, if next index is not defined, we can have two cases:
						//1. Draw gap between this data and next data.
						//2. Connect the next index which could be found as defined.
						//Case 1. If connectNullData is set to false and next data is not
						//defined. We simply continue to next value of the loop
						if (this.params.connectNullData == false && this.axis[k].dataset [m].data [nxt].isDefined == false)
						{
							//Discontinuous plot. So ignore and move to next.
							continue;
						}
						//Now, if nxt data is undefined, we need to find the index of the post data
						//which is not undefined
						if (this.axis[k].dataset [m].data [nxt].isDefined == false)
						{
							//Initiate nxt as -1, so that we can later break if no next defined data
							//could be found.
							nxt = - 1;
							for (j = i + 1; j <= this.num; j ++)
							{
								if (this.axis[k].dataset [m].data [j].isDefined == true)
								{
									nxt = j;
									break;
								}
							}
							//If nxt is still -1, we break
							if (nxt == - 1)
							{
								break;
							}
						}
						//Set line style
						chartMC.lineStyle (this.axis[k].dataset [m].lineThickness, parseInt (this.axis[k].dataset [m].data [i].color, 16) , this.axis[k].dataset [m].data [i].alpha);
						//Now, based on whether we've to draw a normal or dashed line, we draw it
						if (this.axis[k].dataset [m].data [i].dashed)
						{
							//Draw a dashed line
							DrawingExt.dashTo (chartMC, this.axis[k].dataset [m].data [i].x, this.axis[k].dataset [m].data [i].y, this.axis[k].dataset [m].data [nxt].x, this.axis[k].dataset [m].data [nxt].y, this.axis[k].dataset [m].lineDashLen, this.axis[k].dataset [m].lineDashGap);
						} else 
						{
							//Move to the point
							chartMC.moveTo (this.axis[k].dataset [m].data [i].x, this.axis[k].dataset [m].data [i].y);
							//Draw point to next line
							chartMC.lineTo (this.axis[k].dataset [m].data [nxt].x, this.axis[k].dataset [m].data [nxt].y);
						}
						//Get maxY and minY
						maxY = (maxY == undefined || (this.axis[k].dataset [m].data [i].y > maxY)) ? this.axis[k].dataset [m].data [i].y : maxY;
						minY = (minY == undefined || (this.axis[k].dataset [m].data [i].y < minY)) ? this.axis[k].dataset [m].data [i].y : minY;
						//Update loop index (required when connectNullData is true and there is
						//a sequence of empty sets.) Since we've already found the "next" defined
						//data, we update loop to that to optimize.
						i = nxt - 1;
					}
				}
				//Now, we need to adjust the chart movie clip to 0,0 position as center
				chartMC._x = - (this.elements.canvas.w / 2) - this.elements.canvas.x;
				chartMC._y = - (maxY) + ((maxY - minY) / 2);
				//Set the position of holder movie clip now
				holderMC._x = (this.elements.canvas.w / 2) + this.elements.canvas.x;
				holderMC._y = (maxY) - ((maxY - minY) / 2);
				//Apply filter
				this.styleM.applyFilters (holderMC, this.objects.DATAPLOT);
				//Apply animation
				if (this.params.animation)
				{
					this.styleM.applyAnimation (holderMC, this.objects.DATAPLOT, this.macro, holderMC._x, 0, holderMC._y, 0, 100, 100, 100, null);
				}
				//Increment depth
				depth ++;
			}
		}
		//Clear interval
		clearInterval (this.config.intervals.plot);
	}
	/**
	* drawAnchors method draws the anchors on the chart
	*/
	private function drawAnchors () : Void 
	{
		//Variables
		var anchorMC : MovieClip;
		var depth : Number = this.dm.getDepth ("ANCHORS");
		var i : Number, j : Number, k : Number;
		//Create function storage containers for Delegate functions
		var fnRollOver : Function, fnClick : Function;
		//Iterate through all anchors
		for (i = 1; i <= this.numAxis; i++)
		{
			for (j = 1; j <= this.axis[i].numDS; j++)
			{
				if (this.axis[i].dataset [j].drawAnchors)
				{
					for (k = 1; k<= this.num; k++)
					{
						//If defined
						if (this.axis[i].dataset [j].data [k].isDefined)
						{
							//Create an empty movie clip for this anchor
							anchorMC = this.cMC.createEmptyMovieClip ("Anchor_" + i + "_" + j+ "_"+k, depth);
							//Set the line style and fill
							anchorMC.lineStyle (this.axis[i].dataset [j].data [k].anchorBorderThickness, parseInt (this.axis[i].dataset [j].data [k].anchorBorderColor, 16) , 100);
							anchorMC.beginFill (parseInt (this.axis[i].dataset [j].data [k].anchorBgColor, 16) , this.axis[i].dataset [j].data [k].anchorBgAlpha);
							//Draw the polygon
							DrawingExt.drawPoly (anchorMC, 0, 0, this.axis[i].dataset [j].data [k].anchorSides, this.axis[i].dataset [j].data [k].anchorRadius, 90);
							//Set the x and y Position
							anchorMC._x = this.axis[i].dataset [j].data [k].x;
							anchorMC._y = this.axis[i].dataset [j].data [k].y;
							//Set the alpha of entire anchor
							anchorMC._alpha = this.axis[i].dataset [j].data [k].anchorAlpha;
							//Apply animation
							if (this.params.animation)
							{
								this.styleM.applyAnimation (anchorMC, this.objects.ANCHORS, this.macro, anchorMC._x, 0, anchorMC._y, 0, this.axis[i].dataset [j].data [k].anchorAlpha, 100, 100, null);
							}
							//Apply filters
							this.styleM.applyFilters (anchorMC, this.objects.ANCHORS);
							//Event handlers for tool tip
							if (this.params.showToolTip)
							{
								//Create Delegate for roll over function columnOnRollOver
								fnRollOver = Delegate.create (this, dataOnRollOver);
								//Set the index
								fnRollOver.axisIndex = i;
								fnRollOver.dsindex = j;
								fnRollOver.index = k;
								//Assing the delegates to movie clip handler
								anchorMC.onRollOver = fnRollOver;
								//Set roll out and mouse move too.
								anchorMC.onRollOut = anchorMC.onReleaseOutside = Delegate.create (this, dataOnRollOut);
							}
							//Click handler for links - only if link for this anchor has been defined and click URL
							//has not been defined.
							if (this.axis[i].dataset [j].data [k].link != "" && this.axis[i].dataset [j].data [k].link != undefined && this.params.clickURL == "")
							{
								//Create delegate function
								fnClick = Delegate.create (this, dataOnClick);
								//Set index
								fnClick.axisIndex = i;
								fnClick.dsindex = j;
								fnClick.index = k;
								//Assign
								anchorMC.onRelease = fnClick;
							} else 
							{
								//Do not use hand cursor
								anchorMC.useHandCursor = (this.params.clickURL == "") ? false : true;
							}
							//Increase depth
							depth ++;
						}
					}
				}
			}
		}
		//Clear interval
		clearInterval (this.config.intervals.anchors);
	}
	/**
	* drawValues method draws the values on the chart.
	*/
	private function drawValues () : Void 
	{
		//Get value text style
		var valueStyleObj : Object = this.styleM.getTextStyle (this.objects.DATAVALUES);
		//Individual properties
		var isBold : Boolean = valueStyleObj.bold;
		var isItalic : Boolean = valueStyleObj.italic;
		var font : String = valueStyleObj.font;
		var angle : Number = 0;
		//Container object
		var valueObj : MovieClip;
		//Depth
		var depth : Number = this.dm.getDepth ("DATAVALUES");
		//Loop var
		var i : Number, j : Number, k: Number;
		var yPos : Number;
		var align : String, vAlign : String;
		////Iterate through all points
		for (i = 1; i <= this.numAxis; i++)
		{
			for (j = 1; j <= this.axis[i].numDS; j++)
			{
				for (k = 1; k <= this.num; k++)
				{
					//If defined and value is to be shown
					if (this.axis[i].dataset [j].data [k].isDefined && this.axis[i].dataset [j].data [k].showValue)
					{
						//Get the y position based on next data's position
						if (j == 1)
						{
							//For first point, we show the value on top
							vAlign = "top";
							yPos = this.axis[i].dataset [j].data [k].valTBY - this.params.valuePadding;
						} else 
						{
							//If this data value is more than that of previous one, we show textbox above
							if (this.axis[i].dataset [j].data [k].value >= this.axis[i].dataset [j].data [k-1].value)
							{
								//Above
								vAlign = "top";
								yPos = this.axis[i].dataset [j].data [k].valTBY - this.params.valuePadding;
							} else 
							{
								//Below
								vAlign = "bottom";
								yPos = this.axis[i].dataset [j].data [k].valTBY + this.params.valuePadding;
							}
						}
						//Align position
						align = "center";
						//Convey alignment to rendering object
						valueStyleObj.align = align;
						valueStyleObj.vAlign = vAlign;
						//Now, if the labels are to be rotated
						if (this.params.rotateValues)
						{
							valueStyleObj.bold = false;
							valueStyleObj.italic = false;
							valueStyleObj.font = _embeddedFont;
							angle = 270;
						} else 
						{
							//Normal horizontal label - Store original properties
							valueStyleObj.bold = isBold;
							valueStyleObj.italic = isItalic;
							valueStyleObj.font = font;
							angle = 0;
						}
						valueObj = createText (false, this.axis[i].dataset [j].data [k].displayValue, this.cMC, depth, this.axis[i].dataset [j].data [k].x, yPos, angle, valueStyleObj, false, 0, 0);
						//Store the reference to the data array
						this.axis[i].dataset [j].data [k].valueTf = valueObj.tf;
						//Next, we adjust those labels are falling out of top canvas area
						if (((yPos - valueObj.height) <= this.elements.canvas.y))
						{
							//Data value is colliding with the upper side of canvas. So we need to place it within
							//the area
							if ( ! this.params.rotateValues)
							{
								valueObj.tf._y = yPos + (2 * this.params.valuePadding);
							} else 
							{
								valueObj.tf._y = yPos + (2 * this.params.valuePadding) + valueObj.height;
							}
						}
						//Now, we adjust those labels are falling out of bottom canvas area
						if (((yPos + valueObj.height) >= this.elements.canvas.toY))
						{
							//Data value is colliding with the lower side of canvas. So we need to place it within
							//the area
							if ( ! this.params.rotateValues)
							{
								valueObj.tf._y = yPos - (2 * this.params.valuePadding) - valueObj.height;
							} else 
							{
								valueObj.tf._y = yPos - (2 * this.params.valuePadding);
							}
						}
						//Apply filter
						this.styleM.applyFilters (valueObj.tf, this.objects.DATAVALUES);
						//Apply animation
						if (this.params.animation)
						{
							this.styleM.applyAnimation (valueObj.tf, this.objects.DATAVALUES, this.macro, valueObj.tf._x, 0, valueObj.tf._y, 0, 100, null, null, null);
						}
						//Increase depth
						depth ++;
					}
				}
			}
		}
		//Clear interval
		clearInterval (this.config.intervals.dataValues);
	}
	/**
	* drawVDivLines method draws the vertical div lines on the chart
	*/
	private function drawVDivLines () : Void 
	{
		var yPos : Number;
		var depth : Number = this.dm.getDepth ("VDIVLINES");
		//Movie clip container
		var vDivLineMC : MovieClip;
		//Get the horizontal spacing between two vertical div lines
		//We have to accomodate the canvas padding here.
		var horSpace : Number = (this.elements.canvas.w - (2 * this.params.canvasPadding)) / (this.params.numVDivLines + 1);
		//Get x position - add left side canvas padding
		var xPos : Number = this.elements.canvas.x + this.params.canvasPadding;
		var i : Number;
		for (i = 1; i <= this.params.numVDivLines; i ++)
		{
			//Get x position
			xPos = xPos + horSpace;
			//Create the movie clip
			vDivLineMC = this.cMC.createEmptyMovieClip ("vDivLine_" + i, depth);
			//Draw the line
			vDivLineMC.lineStyle (this.params.vDivLineThickness, parseInt (this.params.vDivLineColor, 16) , this.params.vDivLineAlpha);
			if (this.params.vDivLineIsDashed)
			{
				//Dashed line
				DrawingExt.dashTo (vDivLineMC, 0, - this.elements.canvas.h / 2, 0, this.elements.canvas.h / 2, this.params.vDivLineDashLen, this.params.vDivLineDashGap);
			} else 
			{
				//Draw the line keeping 0,0 as registration point
				vDivLineMC.moveTo (0, - this.elements.canvas.h / 2);
				//Normal line
				vDivLineMC.lineTo (0, this.elements.canvas.h / 2);
			}
			//Re-position the div line to required place
			vDivLineMC._x = xPos;
			vDivLineMC._y = this.elements.canvas.y + (this.elements.canvas.h / 2);
			//Apply animation and filter effects to vertical div line
			//Apply animation
			if (this.params.animation)
			{
				this.styleM.applyAnimation (vDivLineMC, this.objects.VDIVLINES, this.macro, vDivLineMC._x, 0, null, 0, 100, null, 100, null);
			}
			//Apply filters
			this.styleM.applyFilters (vDivLineMC, this.objects.VDIVLINES);
			//Increment depth
			depth ++;
		}
		//Clear interval
		clearInterval (this.config.intervals.vDivLines);
	}
	/**
	* drawVGrid method draws the Vertical grid background color
	*/
	private function drawVGrid () : Void 
	{
		//If we're required to draw vertical grid color
		//and numVDivLines > 1
		if (this.params.showAlternateVGridColor && this.params.numVDivLines > 1)
		{
			//Movie clip container
			var gridMC : MovieClip;
			//Loop variable
			var i : Number;
			//Get depth
			var depth : Number = this.dm.getDepth ("VGRID");
			//X Position
			var xPos : Number, xPosEnd : Number;
			var width : Number;
			//Get the horizontal spacing between two vertical div lines
			//We have to accomodate the canvas padding here.
			var horSpace : Number = (this.elements.canvas.w - (2 * this.params.canvasPadding)) / (this.params.numVDivLines + 1);
			for (i = 1; i <= this.params.numVDivLines + 1; i = i + 2)
			{
				//Get x Position
				xPos = this.elements.canvas.x + (i - 1) * horSpace + this.params.canvasPadding;
				//Get x end position
				xPosEnd = xPos + horSpace;
				//If it's first div line, deduct canvas padding from left
				xPos = (i == 1) ? (xPos - this.params.canvasPadding) : xPos;
				//If it's last div line, then we need to add canvas padding to end
				xPosEnd = (i == this.params.numVDivLines + 1) ? (xPosEnd + this.params.canvasPadding) : (xPosEnd);
				//Get the width of the grid.
				width = xPosEnd - xPos;
				//Create the movie clip
				gridMC = this.cMC.createEmptyMovieClip ("VGridBg_" + i, depth);
				//Set line style to null
				gridMC.lineStyle ();
				//Set fill color
				gridMC.moveTo ( - (width / 2) , - (this.elements.canvas.h / 2));
				gridMC.beginFill (parseInt (this.params.alternateVGridColor, 16) , this.params.alternateVGridAlpha);
				//Draw rectangle
				gridMC.lineTo (width / 2, - (this.elements.canvas.h / 2));
				gridMC.lineTo (width / 2, this.elements.canvas.h / 2);
				gridMC.lineTo ( - (width / 2) , this.elements.canvas.h / 2);
				gridMC.lineTo ( - (width / 2) , - (this.elements.canvas.h / 2));
				//End Fill
				gridMC.endFill ();
				//Place it in right location
				gridMC._x = xPosEnd - (width / 2);
				gridMC._y = this.elements.canvas.y + this.elements.canvas.h / 2;
				//Apply animation
				if (this.params.animation)
				{
					this.styleM.applyAnimation (gridMC, this.objects.VGRID, this.macro, gridMC._x, 0, null, 0, 100, 100, 100, null);
				}
				//Apply filters
				this.styleM.applyFilters (gridMC, this.objects.VGRID);
				//Increase depth
				depth ++;
			}
		}
		//Clear interval
		clearInterval (this.config.intervals.vGrid);
	}
	/**
	* drawLegend method renders the legend
	*/
	private function drawLegend () : Void
	{
		if (this.params.showLegend)
		{
			this.lgnd.render ();
			//Apply filter
			this.styleM.applyFilters (lgndMC, this.objects.LEGEND);
			//Apply animation
			if (this.params.animation)
			{
				this.styleM.applyAnimation (lgndMC, this.objects.LEGEND, this.macro, null, 0, null, 0, 100, null, null, null);
			}
		}
		//Clear interval
		clearInterval (this.config.intervals.legend);
	}
	/**
	* setContextMenu method sets the context menu for the chart.
	* For this chart, the context items are "Print Chart".
	*/
	private function setContextMenu () : Void 
	{
		var chartMenu : ContextMenu = new ContextMenu ();
		chartMenu.hideBuiltInItems ();
		//Create a print chart contenxt menu item
		var printCMI : ContextMenuItem = new ContextMenuItem ("Print Chart", Delegate.create (this, printChart));
		//Push print item.
		chartMenu.customItems.push (printCMI);
		if (this.params.imageSave){
			//Add the export to image option
			chartMenu.customItems.push(super.returnImageSaveMenuItem());		
		}
		//Push "About FusionCharts" Menu Item
		if (this.params.showFCMenuItem){
			//Push "About FusionCharts" Menu Item
			chartMenu.customItems.push(super.returnAbtMenuItem());		
		}
		//Assign the menu to cMC movie clip
		this.cMC.menu = chartMenu;
	}
	// -------------------- EVENT HANDLERS --------------------//
	/**
	* check is the delegat-ed event handler method that'll
	* be invoked when the user checks any of the checkBox.
	* This function is invoked, only if the checkBox is to be shown.
	* Here, we show the tool tip.
	*/
	private function onCheck(evtObj) : Void 
	{
		//Loop Variable
		var i:Number, j:Number;
		for(i= 1;i<=this.axis[evtObj.index].numDS;i++) {
			//get the  movie clip reference
			var holderMC : MovieClip = eval(this.cMC+".ChartHolder_" + i+ "_"+evtObj.index);
			holderMC._visible = evtObj.state;
			if (this.axis[evtObj.index].dataset [i].drawAnchors)
			{
				for (j = 1; j<= this.num; j++)
				{
					//If defined
					if (this.axis[evtObj.index].dataset [i].data [j].isDefined)
					{
						//Get the reference for this anchor
						var anchorMC:MovieClip = eval(this.cMC+".Anchor_" + evtObj.index + "_" + i+ "_"+j);
						anchorMC._visible = evtObj.state;
						//Make the value TF hidden
						this.axis[evtObj.index].dataset [i].data [j].valueTf._visible = evtObj.state;
					}
				}
			}
		}
		
	}
	/**
	* onAxisClick is the delegat-ed event handler method that'll
	* be invoked when the user clicks any of the Axis.
	* This function is invoked, only if the axis is to be shown.
	* Here, we show the tool tip.
	*/
	private function onAxisClick(evtObj) : Void 
	{
		//Loop Variable
		var i:Number = 0;
		var tmp:Number, lastWidth:Number= 0, lastX:Number = 0, lastY:Number = 0, currentWidth:Number=  0;
		//Flag 
		var flag:Boolean = false;
		//objects
		var pos1:Object;
		//Check all the left Array index and is not the primary Axis
		if(this.leftAxisArray[this.leftAxisArray.length-1] != evtObj.index && this.numAxisOnLeft>1 ) {
			//Loop all index of the left Array
			for(i =1;i<=this.numAxisOnLeft;i++) {
				//check if the current Id is on the left of Canvas
				if(this.leftAxisArray[i-1] == evtObj.index && !flag) {
					//Get the position of the swaping index
					pos1 = this.vAxis[evtObj.index].getPosition();
					//store the width
					lastWidth = this.vAxis[evtObj.index].getWidth();
					//Width of the primary Axis
					currentWidth = this.vAxis[this.leftAxisArray[this.numAxisOnLeft-1]].getWidth();
					//Set the primary Axis position with the clicked Axis
					this.vAxis[this.leftAxisArray[this.numAxisOnLeft-1]].setPosition(pos1.x-lastWidth+currentWidth, pos1.y);
					//Set the new position of the axis as the last position known
					lastX = pos1.x-lastWidth+currentWidth;
					lastY = pos1.y;
					//Show the divlines of the clicked Axis
					this.vAxis[evtObj.index].showDivLines();
					//Set the clicked Axis as the left primary Axis
					this.vAxis[evtObj.index].setPrimary(true);
					//Set the left Secondary Axis
					this.vAxis[this.leftAxisArray[this.numAxisOnLeft-1]].setPrimary(false);
					//Hide the divLines of the last 
					this.vAxis[this.leftAxisArray[this.numAxisOnLeft-1]].hideDivLines();
					//Swap the index in the left array
					tmp = this.leftAxisArray[i-1];
					this.leftAxisArray[i-1] = this.leftAxisArray[this.numAxisOnLeft-1];
					this.leftAxisArray[this.numAxisOnLeft-1] = tmp;
					//Also hide all the primary rightAxis DivLines - if there is any 
					this.vAxis[this.rightAxisArray[0]].hideDivLines();
					//Flag to indicate one of the left Axis has been Shifted
					flag = true;
					continue;
				}
				if(flag && i!=this.numAxisOnLeft) {
					//for all the other non primary left axis set the position relative to the primary left axis
					currentWidth = this.vAxis[this.leftAxisArray[i-1]].getWidth();
					this.vAxis[this.leftAxisArray[i-1]].setPosition(lastX+currentWidth+this.AXIS_PADDING, lastY);
					//Set the current Axis position as the last known axis
					lastX = lastX+currentWidth+this.AXIS_PADDING;
				}
			}
			if(flag ) {
				//Set the clicked axis position as the last index of the left Array and set the position acc.
				currentWidth = this.vAxis[evtObj.index].getWidth();
				this.vAxis[evtObj.index].setPosition(lastX+currentWidth+this.AXIS_PADDING, lastY);
			}
		
		} else {
			//Also hide all the rightAxis DivLines
			this.vAxis[this.rightAxisArray[0]].hideDivLines();
			this.vAxis[this.leftAxisArray[this.leftAxisArray.length-1]].showDivLines();
		}
		if(this.rightAxisArray[0] != evtObj.index  && this.numAxisOnRight>1 ) {
			flag = false;
			//Loop all index of the right Array
			for(i =1;i<=this.numAxisOnRight;i++) {
				//check if the current Id is on the left of Canvas
				if(this.rightAxisArray[i-1] == evtObj.index && !flag) {
					//Get the position of the swaping indexes
					pos1 = this.vAxis[this.rightAxisArray[0]].getPosition();
					this.vAxis[evtObj.index].setPosition(pos1.x, pos1.y);
					lastX = pos1.x;
					lastWidth = this.vAxis[evtObj.index].getWidth();
					lastY = pos1.y;
					this.vAxis[evtObj.index].showDivLines();
					//Set the left primary Axis
					this.vAxis[evtObj.index].setPrimary(true);
					//Set the left primary Axis
					this.vAxis[this.rightAxisArray[0]].setPrimary(false);
					this.vAxis[this.rightAxisArray[0]].hideDivLines();
					//Swap the index in the array
					tmp = this.rightAxisArray[i-1];
					this.rightAxisArray[i-1] = this.rightAxisArray[0];
					this.rightAxisArray[0] = tmp;
					//Also hide all the LeftAxis DivLines
					this.vAxis[this.leftAxisArray[this.leftAxisArray.length-1]].hideDivLines();
					flag = true;
					break;
				}
			}
			//Loop all index of the right Array
			for(i =2;i<=this.numAxisOnRight;i++) {
				if(flag) {
					this.vAxis[this.rightAxisArray[i-1]].setPosition(lastX+lastWidth+this.AXIS_PADDING, lastY);
					lastX = lastX+lastWidth+this.AXIS_PADDING;
					lastWidth = this.vAxis[this.rightAxisArray[i-1]].getWidth();
				}
			}
		}  else if (!flag) {
			//Also hide all the leftAxis DivLines
			this.vAxis[this.leftAxisArray[this.leftAxisArray.length-1]].hideDivLines();
			this.vAxis[this.rightAxisArray[0]].showDivLines();
		}
	}
	/**
	* dataOnRollOver is the delegat-ed event handler method that'll
	* be invoked when the user rolls his mouse over an anchor.
	* This function is invoked, only if the tool tip is to be shown.
	* Here, we show the tool tip.
	*/
	private function dataOnRollOver () : Void 
	{
		//Index of data is stored in arguments.caller.index
		var axisIndex: Number = arguments.caller.axisIndex;
		var dsindex : Number = arguments.caller.dsindex;
		var index : Number = arguments.caller.index;
		//Set tool tip text
		this.tTip.setText (this.axis[axisIndex].dataset [dsindex].data [index].toolText);
		//Show the tool tip
		this.tTip.show ();
	}
	/**
	* dataOnRollOut method is invoked when the mouse rolls out
	* of anchor. We just hide the tool tip here.
	*/
	private function dataOnRollOut () : Void 
	{
		//Hide the tool tip
		this.tTip.hide ();
	}
	/**
	* dataOnClick is invoked when the user clicks on a anchor (if link
	* has been defined). We invoke the required link.
	*/
	private function dataOnClick () : Void 
	{
		//Index of column is stored in arguments.caller.index
		var axisIndex: Number = arguments.caller.axisIndex;
		var dsindex : Number = arguments.caller.dsindex;
		var index : Number = arguments.caller.index;
		//Invoke the link
		super.invokeLink (this.axis[axisIndex].dataset [dsindex].data [index].link);
	}
	/**
	* reInit method re-initializes the chart. This method is basically called
	* when the user changes chart data through JavaScript. In that case, we need
	* to re-initialize the chart, set new XML data and again render.
	*/
	public function reInit () : Void 
	{
		//Invoke super class's reInit
		super.reInit ();
		//Now initialize things that are pertinent to this class
		//but not defined in super class.
		this.categories = new Array ();
		this.lgnd.reset ();
		//Initialize the axis array
		this.axis = new Array ();
		//Axis array Object
		this.vAxis= new Array ();
		//Initialize the index Array
		this.leftAxisArray = new Array();
		this.rightAxisArray = new Array();
		//Initialize the number of data elements present
		this.numAxis = 0;
		//Initialize the Numbers
		this.numAxisOnRight = 0;
		this.numAxisOnLeft = 0;
		this.config.paddingReqd = false;
		this.num = 0;
		this.maxDS = 0;
	}
	/**
	* remove method removes the chart by clearing the chart movie clip
	* and removing any listeners.
	*/
	public function remove () : Void 
	{
		//Remove each of the drawn Axis
		//Now scan through the Axis list
		for (var i:Number = 1; i <= this.numAxis; i++)
		{	
			//Remove all the listeners
			this.vAxis[i].removeEventListener("onCheck", this.checkListener);
			this.vAxis[i].removeEventListener("onAxisClick", this.clickListener);
			//Destroy the object
			this.vAxis[i].destroy();
		}
		super.remove ();
		//Remove class pertinent objects
		this.lgnd.destroy ();
		lgndMC.removeMovieClip ();
		//Remove the cursor MC
		cursorMC.removeMovieClip();
	}
}


